        ASEG
        .phase  08050h-7

        ;header for .BIN file
        DB      0FEh
        DW      08050h
        DW      ENDOFCODE-1
        DW      START

START: 

; ---------------------------------------------------------------------        

; --------------------- BEGIN | DECLARATION OF CONSTANTS
BUFFERADDR			EQU 0BD00H
BUFGUI				EQU 0B000H
BDOS				EQU 0F37DH
FCB					EQU 0BB00H
DMADISK				EQU 04000H
DMA					EQU 0B500H
BUFFERIX			EQU 0B000H
DEBUG_REG			EQU 0B100H
DEBUG_SLOT			EQU 0B200H
DEBUG_TRACK			EQU 0B300H
; --------------------- END | DECLARATION OF CONSTANTS	
	
    JP PULA

; --------------------- BEGIN | DECLARATION OF VARIABLES
BARRAMAX:			DW 0
BARRAVALUE: 		DW 0
BARRAVALFILE: 		DW 0
BARRAVALDISK: 		DW 0
BARRADDR: 			DW 0
BARRAPOS:			DB 0
BARRATUAL:			DB 0
BARRAFLAG:			DB 0
REG_ADDR:			DW 0,0,0,0,0,0,0,0,0,0,0
					DW 0,0,0,0,0,0,0,0,0,0,0
					DW 0,0,0,0,0,0,0,0,0,0,0
					DW 0,0,0,0,0,0,0,0,0,0,0
POWERPROCOPC:		DB 0
DRIVESTATUS:		DB 0
ACTUALSIDE:			DB 0
PAGE0RAM:			DB 0
PAGE1RAM:			DB 0
STMAPTEMP:			DW 0
SELECTMEMO:			DB 0
SLOTMEGARAM:		DW 0
SLOTMAPPER:			DW 0
DOS2:				DB 0
ACTUALPAGE:			DB 0
CHOOSEPOSITION:		DB 0
INTERFACEADDRDOS:	DW 0
INTERFACEADDR:		DW 0
INTERFACES:			DW 0,0
INTERFACESLOTS:		DW 0,0,0,0
INTERFACESLOTADDR:	DW 0,0
QTDINTERFACES:		DB 0
DEBUG1D:			DW 0
DEBUG2D:			DW 0
DEBUG1F:			DW 0
DEBUG2F:			DW 0
REG32:		 		DW 0,0
ADDRVRAM: 			DW 0
DIGADDR:			DW 0
BCDBUF:				DS 8
BUFNUM:				DW 0,0,0,0,0,0,0,0,0,0
BUFEND:
SUBINTER:			DB 0
QTDBLOCKS:			DW 0
BLOCKADDR:			DW 0
DRIVETOSHIBA:		DB 0
TRACKSECTOR:		DW 0
READWRITE:			DB 0
STR_DOS2:			DB 11,'MSXDOS2.SYS' 
STR_DOS2M:			DB 11,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH
STR_TOSHIBA1:		DB 13,3AH,0,7FH,0E6H,0C0H,0FEH,80H,20H,0F7H,7EH,32H,0,7FH
STR_TOSHIBA1M:		DB 13,0FFH,0,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0FFH,0,0FFH
STR_TOSHIBA2:		DB 9,3EH,2,32H,0,7FH,3CH,32H,0,7FH
STR_TOSHIBA2M:		DB 9,0FFH,0FFH,0FFH,0,0FFH,0FFH,0FFH,0,0FFH
STR_TOSHIBA3:		DB 6,3EH,4,32H,0,7FH,0C9H
STR_TOSHIBA3M:		DB 6,0FFH,0FFH,0FFH,0,0FFH,0FFH
STR_PHILIPS:		DB 13,0E5H,0D5H,0C5H,1,0,7FH,0,0,0,0,32H,0,7FH
STR_PHILIPSM:		DB 13,0FFH,0FFH,0FFH,0FFH,0,0FFH,0,0,0,0,0FFH,0,0FFH
STR_PHILIPS1:		DB 13,0E5H,0D5H,0C5H,0,0,0,0,1,0,7FH,32H,0,7FH
STR_PHILIPS1M:		DB 13,0FFH,0FFH,0FFH,0,0,0,0,0FFH,0,0FFH,0FFH,0,0FFH
STR_PHIL_NAT:		DB 17,0E5H,0D5H,0C5H,1,0,7FH,0,0,0,0,0,0,0,0,32H,0,7FH
STR_PHIL_NATM:		DB 17,0FFH,0FFH,0FFH,0FFH,0,0FFH,0,0,0,0,0,0,0,0,0FFH,0,0FFH
STR_PHILIPS2:		DB 3,32H,0FDH,7FH
STR_PHILIPS2M:		DB 3,0FFH,0FFH,0FFH
STR_NATIONAL:		DB 5,32H,0,7FH,0E3H,0E3H
STR_NATIONALM:		DB 5,0FFH,0,0FFH,0FFH,0FFH
STR_MICROSOL:		DB 4,03EH,0D0H,0D3H,0D0H
STR_MICROSOLM:		DB 4,0FFH,0FFH,0FFH,0FFH
CMD_SEEK:			DB 3,0FH,0,0
CMD_READ:			DB 9,46H,0,0,0,8,2,9,50H,0FFH
CMD_SENSE_INT:		DB 1,8
CMD_SENSE_DEV:		DB 2,4,0
CMD_RECALIBRATE:	DB 2,7,0	
KBYTESTEMP:			DW 0
KBYTESTOTAL:		DW 0
KBYTESLOADED:		DW 0
TATUALBUF:			DB 0
TATUAL1:			DB 0
TATUAL2:			DB 0
DRIVEDOS:			DW 0
BASENUM:			DB '0123456789'
BLOCKI:				DW 0
REG_CMD:			DW 0
REG_TRACK:			DW 0
REG_SECTOR:			DW 0
REG_DATA:			DW 0
REG_SIDE:			DW 0
REG_DRIVE:			DW 0
REG_STATUS:			DW 0
REG_STATUS_2:		DW 0
REG_RESULT_STATUS:	DW 0
REG_CONTROL0:		DW 0
REG_CONTROL1:		DW 0	
SEQUENCIA:			DB 0,0,0
INTERFACE:			DB 0
SLOT1:				DB 0
SLOT2:				DB 0
SUBSLOT:			DB 0
CMDS:				DW 0
DRIVELADO:			DW 0
INTERDISCO:			DW 0
INTERDISK:			DB 0
ADDRESS:			DW 0	
PHELPH:				DW 0
CFRENTE:			DB 0
CFUNDO:				DB 0
HLTEMP:				DW 0
FCURSOR:			DB 0
AFTEMP:				DW 0
SETI:				DW 0
SIT:				DW 0
NSET:				DB 0
NST:				DB 0
ADDR:				DW 0
PAGEM:				DB 0
PAGEG:				DB 0
BLOCOS:				DB 0
BLOCOT:				DB 0
FBLOC:				DB 0
BLOCI:				DW 0
FFILE:				DB 0
TSET:				DW 0
QBLC:				DW 0
QBLC2:				DW 0
TEMP:				DW 0
DRIVE:				DB 0
TOPER:				DB 0
FACES:				DB 0
TCOPIA:				DB 0
OVER:				DB 0
DRIVE1:				DB 0
DRIVE2:				DB 0
ACESSO:				DB 0
DRIVED:				DB 0
OPERAC:				DB 0
DRV:				DB 0
OP:					DB 0
ATUAL:				DB 0
PNUM:				DW 0
PEND:				DW 0
LADO:				DB 0
ENDER:				DW 0
CURSOR:				DW 0
TPAGES:				DW 0
TPAGESM:			DW 0
TMEMO:				DB 0
TMEMOM:				DB 0
CAB:				DB 0
TR:					DB 0
FLAGB:				DB 0
LDATUAL:			DB 0
TATUAL:				DB 0
DRVBAK:				DB 0
CHECKCALC:			DW 0
HEXNUMBERS:			DB '0123456789ABCDEF'
LOGICSECT: 			DW 0
SIDES: 				DB 0
RAWTRACK: 			DB 0
TRACK: 				DB 0
SIDE:				DB 0
; --------------------- END | DECLARATION OF VARIABLES	

; --------------------- BEGIN | DEFINITION OF MESSAGES
MSGWAIT:			DB 'WAIT!',0
MSGFDC:				DB 'IDENTIFYING FLOPPY DISK CONTROLER...',0
MSGPAR:				DB '(BYTES LOADED)',0
MSGPAR2:			DB '(BYTES SAVED)',0
MSGLA:				DB 'READING FILE       ',0
MSGLS:				DB 'READING SECTORS    ',0
MSGGA:				DB 'WRITING FILE       ',0
MSGGS:				DB 'WRITING SECTORS    ',0
MSGCLEAR:			DB '                       ',0
MSGO1:				DB ';insert;source;disk;   ',0
MSGD:				DB ';insert;dest',1,';disk;   ',0
NOME:				DB 1,'FILE    DSK'
MSG1A:				DB '40T - SINGLE ',0
MSG1B:				DB '40T - DOUBLE ',0
MSG1C:				DB '80T - SINGLE ',0
MSG1D:				DB '80T - DOUBLE ',0
MSG2A:				DB 'DISK -> FILE ',0
MSG2B:				DB 'FILE -> DISK ',0
MSGSLOT:			DB '(SLOT    )',0
MSG5A:				DB 'BIOS         ',0   
MSG5B1:				DB 'W.DIGITAL FDC',0	
MSG5B2:				DB 'FUJITSU FDC  ',0
MSG5B3:				DB 'TOSHIBA FDC  ',0
MSG5B4:				DB 'MICROSOL FDC ',0
MSGFDC0:			DB 'W.DIGITAL?',0
MSGFDC1:			DB 'W.DIGITAL',0
MSGFDC2:			DB 'FUJITSU',0
MSGFDC3:			DB 'TOSHIBA',0
MSGFDC4:			DB 'MICROSOL',0
MSG6A:				DB 'NO ',0
MSG6B:				DB 'YES',0
BAR1:				DB ' FORMAT       ',0
BAR2:				DB ' OPERATION    ',0
BAR3:				DB ' SOURCE DRIVE ',0
BAR4:				DB ' DEST. DRIVE  ',0
BAR5:				DB ' ACCESS TYPE  ',0
BAR6:				DB ' EXTRA TRACKS ',0
BAR7:				DB ' FILENAME     ',0
MSG_NO_MEMORY:		DB 'MEMORY EXPANSIONS DISABLED! '
MSG_MEGARAM:		DB '     MEGARAM SELECTED!      '
MSG_MAPPER:			DB '  MEMORY MAPPER SELECTED!   '
MSG_DEFAULT:		DB '                            ',13,13
					DB 'PRESS ;any;key; TO RETURN',0
MSGHELP:			DB '  [1]    CHANGE FOREGROUND COLOR',13
					DB '  [2]    CHANGE BACKGROUND COLOR',13
					DB '  [H]    SHOW THIS HELP',13
					DB '  [N]    DEFINE DSK FILE NAME',13
					DB ' [ESC]   ABORT-QUIT PROCESS',13
					DB '[RETURN] START PROCESS',13,13
					DB '   ',0F9H,' ;auxilliary;routines; ',0F9H,13,13	
					DB '  RUDOLF ARTHUR FRANS GUTLICH',13,13				
					DB '     ',0F9H,' ;official;testers; ',0F9H,13,13					
					DB '    ULISSES ARAUJO BIAZON',13
					DB '    WERNER AUGUSTO RODER KAI',13
					DB '    FERNANDO MANUEL GARCIA',13,13					
					DB '   PRESS ;any;key; TO RETURN',0
DISPMEM:			DB ';special;memory;found;',13
					DB ' TYPE:              ',13
					DB ' SLOT:  - ',13
					DB ' SIZE:      KBYTES',0
ANYKEY:				DB 'PRESS ;any;key; TO CONTINUE',0
DISPMAP:			DB 'MEMORY MAPPER'
DISPMEG:			DB 'MEGARAM      '
SUMMSG:				DB ' ;digital;certificate;',13,13
					DB ' CHECKSUM NUMBER:',13,13
					DB 'PRESS ;any;key; TO RETURN',0
PROTMSG:			DB 'THE DISK IS ;write; PROTECTED',13,13
					DB '   PRESS ANY KEY TO RETURN',0					
; --------------------- END | DEFINITION OF MESSAGES
	
; --------------------- BEGIN | MAIN LOOP
PULA:		LD A,22
			LD (BARRAPOS),A
			LD HL,774
			LD (BARRADDR),HL
			LD A,0FCH
			LD (TCOPIA),A
			LD A,0
			LD (OVER),A
			LD A,0
			LD (OPERAC),A
			LD A,0
			LD (DRIVE1),A
			LD (DRIVE2),A
			LD A,0
			LD (ACESSO),A
			LD A,1
			LD (ATUAL),A
			LD A,3
			LD (SELECTMEMO),A
			CALL INITSLOT	
			CALL GUI
			CALL FINDDOS2
			CALL IDENTINTERFACES
RESTOREINT:	LD HL,DEBUG_TRACK
			LD DE,DEBUG_TRACK + 1
			LD BC,7FH
			LD A,0FFH
			LD (HL),A
			LDIR			
COMECO:		LD HL,DEBUG_TRACK
			LD (DEBUG1D),HL
			LD HL,DEBUG_TRACK + 18H
			LD (DEBUG2D),HL
			LD HL,DEBUG_TRACK + 30H
			LD (DEBUG1F),HL
			LD HL,DEBUG_TRACK + 48H
			LD (DEBUG2F),HL
			LD HL,0
			LD (BARRAVALDISK),HL
			LD (BARRAVALFILE),HL
			XOR A
			LD (BARRAFLAG),A
			LD (DRIVESTATUS),A			
			LD (ACTUALSIDE),A	
			CALL PRINTWAIT1			
			CALL RESTORE	
			CALL PRINTWAIT2	
			CALL CLEARINFO			
			CALL OP5B
			CALL PBARRA
			CALL MENU
			CALL LBARRA
			CALL CHKMEM
			CALL SETMEMO
			CALL ACOPIA
			CALL CTBLOC
			XOR A
			LD (FBLOC),A
			LD (BLOCOT),A
			LD (FFILE),A
			LD (LDATUAL),A
			LD HL,0
			LD (SETI),HL
			LD (BLOCI),HL
			LD (BLOCKI),HL	
			LD (KBYTESLOADED),HL
			LD (CHECKCALC),HL
			CALL GETQTDBLOCKS
			CALL ST
			CALL WINDOWCRC
			JR COMECO
; --------------------- END | MAIN LOOP
	
; --------------------- BEGIN | MAIN PROCEDURE OF READ/WRITE OPERATIONS	
ST:			LD A,(TCOPIA)
			CP 0FCH
			JR Z,STFC
			CP 0FDH
			JR Z,STFD
			CP 0F8H
			JR Z,STF8
			LD HL,2879
			LD (BARRAMAX),HL
			JR STCONT
STFC:		LD HL,719
			LD (BARRAMAX),HL
			JR STCONT
STFD:		LD HL,1439
			LD (BARRAMAX),HL	
			JR STCONT
STF8:		LD HL,1439
			LD (BARRAMAX),HL	
STCONT:		CALL STELA
			CALL MEMFCK
			CALL C,MEMF
STLOP:		CALL CKBLOC
			LD (BLOCOS),A
			PUSH AF
			POP HL
			LD (AFTEMP),HL
			LD A,(OPERAC)
			AND A
			JR NZ,ST1
			CALL LER
			CALL LENDOS
			CALL DISK
			CALL DESLIGADRIVE			
			LD HL,(AFTEMP)
			PUSH HL
			POP AF
			LD (BLOCOS),A	
			CALL GRAVAR
			LD A,2
			LD (POWERPROCOPC),A
			CALL POWERPROC				
			CALL GRAA 
			CALL FILE
			LD A,1
			LD (POWERPROCOPC),A
			CALL POWERPROC			
			LD A,(FBLOC)
			CP 1
			JR NZ,STLOP
			JP RTELA
ST1:		CALL LER
			CALL LENDOA		
			CALL FILE
			LD A,1
			LD (POWERPROCOPC),A			
			CALL POWERPROC			
			LD HL,(AFTEMP)
			PUSH HL
			POP AF
			LD (BLOCOS),A
			CALL GRAVAR
			CALL GRAS
			CALL DISK
			CALL DESLIGADRIVE
			LD A,(FBLOC)
			CP 1
			JP NZ,STLOP
			JP RTELA
; --------------------- END | MAIN PROCEDURE OF READ/WRITE OPERATIONS
	
; --------------------- BEGIN | MAIN PROCEDURE TO READ/WRITE SECTORS
DISK:		DI
			LD A,(OPERAC)
			AND A
			JR Z,DRVORIGEM
			LD A,(TATUAL2)
			LD C,A
			LD B,4
			JR DRVORIGPUL
DRVORIGEM:	LD A,(TATUAL1)
			LD C,A
			LD B,4
DRVORIGPUL:	LD HL,(DEBUG1D)
			LD A,C
			LD (HL),A
			INC HL
			LD (DEBUG1D),HL
			CALL CHAVEIA
			LD A,4
			LD (PAGEM),A
			XOR A
			LD (PAGEG),A
			LD (FLAGB),A
LOOP:		CALL CNSET
			CALL INCMEM
			CALL SETOR
			;CALL TRANSF
			LD HL,(SETI)
			LD DE,20H
			ADD HL,DE
			LD (SETI),HL
			LD A,(BLOCOS)
			DEC A
			LD (BLOCOS),A
			JR NZ,LOOP
			LD A,(ACESSO)
			AND A
			JR Z,ABIOS2
			LD A,(TATUAL)
			LD C,A
			JR DISKC
ABIOS2:		LD B,9
			CALL CHAVEIA
			LD A,(TATUALBUF)
			LD C,A	
DISKC:		LD A,(DRIVE1)
			LD B,A
			LD A,(DRIVE2)
			CP B
			JR NZ,NOTIQUAL
			LD A,C
			LD (TATUAL1),A
			LD (TATUAL2),A
			JR DISKOUT
NOTIQUAL:	LD A,(OPERAC)
			AND A
			JR Z,SETDRVORG
			LD A,C
			LD (TATUAL2),A
			JR DISKOUT
SETDRVORG:	LD A,C
			LD (TATUAL1),A
DISKOUT:	LD HL,(DEBUG2D)
			LD (HL),A
			INC HL
			LD (DEBUG2D),HL
			RET
;------------ END | MAIN PROCEDURE TO READ/WRITE SECTORS

;------------ BEGIN | MAIN PROCEDURE TO READ/WRITE FILE
FILE:		LD A,(OPERAC)
			AND A
			JR Z,DRVDESTINO
			LD A,(TATUAL1)
			LD C,A
			LD B,4
			JR DRVDESTPUL
DRVDESTINO:	LD A,(TATUAL2)
			LD C,A
			LD B,4
DRVDESTPUL:	LD HL,(DEBUG1F)
			LD A,C
			LD (HL),A
			INC HL
			LD (DEBUG1F),HL
			CALL CHAVEIA
			LD A,4
			LD (PAGEM),A
			XOR A
			LD (PAGEG),A
			LD A,1
			LD (FLAGB),A
;------------ BEGIN | ROUTINE TO CHECK IF IT IS FIRST TIME		
			LD A,(FFILE)
			AND A
			JR NZ,SFLOOP
			LD A,1
			LD (FFILE),A
;------------ END | ROUTINE TO CHECK IF IT IS FIRST TIME	
;------------ BEGIN | ROUTINE TO DELETE/CREATE/CLOSE NEW FILE	
			LD A,(OPERAC)
			AND A
			JR NZ,SFLOOP
			CALL LIMPA
			LD HL,NOME
			LD DE,FCB
			LD BC,12
			LDIR
			LD DE,FCB
			LD C,13H
			CALL BDOS
			LD DE,FCB
			LD C,16H
			CALL BDOS
			LD DE,FCB
			LD C,10H
			CALL BDOS
;------------ END | ROUTINE TO DELETE/CREATE/CLOSE NEW FILE				
;------------ BEGIN | ROUTINE TO OPEN FILE				
SFLOOP:		LD HL,DMADISK
			LD (BLOCKADDR),HL
			CALL LIMPA
			LD HL,NOME
			LD DE,FCB
			LD BC,12
			LDIR
			LD DE,FCB
			LD C,0FH
			CALL BDOS	
;------------ END | ROUTINE TO OPEN FILE			
			CALL CNSET
			CALL INCMEM
			LD HL,128
			LD (QBLC2),HL
;------------ BEGIN | ROUTINE TO SET UP FILE OPERATIONS		
LOOPFILE:	LD B,3
			CALL CKESC
			LD DE,DMA
			LD C,1AH
			CALL BDOS
			LD HL,128
			LD (FCB + 14),HL
			LD HL,0
			LD (FCB + 33),HL
			LD (FCB + 35),HL
			LD HL,(BLOCKI)
			LD (FCB + 33),HL
;------------ END | ROUTINE TO SET UP FILE OPERATIONS				
			LD A,(OPERAC)
			AND A
			JR NZ,SFLOAD
;------------ BEGIN | ROUTINE TO WRITE FILE
			CALL SETACTUALPAGE
			LD HL,(BLOCKADDR)
			LD DE,DMA	
			LD BC,128
			LDIR
			LD HL,1
			LD DE,FCB
			LD C,26H
			CALL BDOS		
			JR SFPUL
;------------ END | ROUTINE TO WRITE FILE			
;------------ BEGIN | ROUTINE TO READ FILE			
SFLOAD:		LD HL,1
			LD DE,FCB
			LD C,27H
			CALL BDOS
			CALL SETACTUALPAGE		
			LD HL,DMA
			LD DE,(BLOCKADDR)	
			LD BC,128
			LDIR
			LD IX,DMA
			LD HL,(CHECKCALC)
			LD BC,128
			CALL CHECKSUM
			LD (CHECKCALC),HL			
;------------ END | ROUTINE TO READ FILE
SFPUL:		LD HL,(BLOCKI)
			PUSH HL
			CALL DIV2
			CALL DIV2	
			LD (BARRAVALFILE),HL
			LD DE,(BARRAVALDISK)
			ADD HL,DE
			INC HL
			INC HL
			LD (BARRAVALUE),HL
			CALL PROGRESSBAR
			POP HL
			INC HL
			LD (BLOCKI),HL
;------------ BEGIN | ROUTINE TO CHECK END OF COPY
			LD HL,(QTDBLOCKS)
			DEC HL
			LD (QTDBLOCKS),HL	
			LD A,L
			OR H
			JR Z,FIMARQUIVO0
;------------ END | ROUTINE TO CHECK END OF COPY	
;------------ BEGIN | ROUTINE TO CHECK IF IT IS THE LAST BLOCK (001-128 = 16K)			
			LD HL,(QBLC2)
			DEC HL
			LD (QBLC2),HL
			LD A,L
			OR H
			JR Z,FIMARQUIVO
;------------ END | ROUTINE TO CHECK IF IT IS THE LAST BLOCK (001-128 = 16K)			
;------------ BEGIN | ROUTINE TO INCREASE RAM ADDRESS AND PRINT SIZE FILE
			LD HL,(BLOCKADDR)
			LD DE,128
			ADD HL,DE
			LD (BLOCKADDR),HL
			CALL PPART
			JP LOOPFILE
;------------ END | ROUTINE TO INCREASE RAM ADDRESS AND PRINT SIZE FILE			
FIMARQUIVO0:LD HL,(QBLC2)
			DEC HL
			LD (QBLC2),HL
FIMARQUIVO:	LD HL,128
			LD DE,(QBLC2)
			XOR A
			SBC HL,DE
			CALL DIV2
			CALL DIV2
			CALL DIV2
			PUSH HL
			LD HL,(KBYTESLOADED)
			POP DE
			ADD HL,DE
			LD (KBYTESLOADED),HL
;------------ BEGIN | ROUTINE TO CLOSE FILE			
			LD DE,FCB
			LD C,10H
			CALL BDOS	
;------------ END | ROUTINE TO CLOSE FILE		
;------------ BEGIN | ROUTINE TO CHECK IF THERE IS ONE MORE 16K MEMORY BLOCK	
			LD A,(BLOCOS)
			DEC A
			LD (BLOCOS),A
			JP NZ,SFLOOP
;------------ END | ROUTINE TO CHECK IF THERE IS ONE MORE 16K MEMORY BLOCK		
;------------ BEGIN | ROUTINE TO SAVE HEAD DRIVE POSITION	
			LD A,(DRIVE1)
			LD B,A
			LD A,(DRIVE2)
			CP B
			JR NZ,NOTIQUAL2
			LD B,9
			CALL CHAVEIA
			LD A,(TATUALBUF)
			LD (TATUAL1),A
			LD (TATUAL2),A
			LD (TATUAL),A
			JR FILEOUT	
NOTIQUAL2:	LD A,(OPERAC)
			AND A
			JR Z,SETDRVDEST
			LD B,9
			CALL CHAVEIA
			LD A,(TATUALBUF)
			LD (TATUAL1),A
			JR FILEOUT
SETDRVDEST:	LD B,9
			CALL CHAVEIA
			LD A,(TATUALBUF)
			LD (TATUAL2),A	
;------------ END | ROUTINE TO SAVE HEAD DRIVE POSITION	
;------------ BEGIN | ROUTINE TO SAVE ALL HEAD DRIVE POSITIONS FOR DEBUG PURPOSES
FILEOUT:	LD HL,(DEBUG2F)
			LD (HL),A
			INC HL
			LD (DEBUG2F),HL			
			RET
;------------ END | ROUTINE TO SAVE ALL HEAD DRIVE POSITIONS FOR DEBUG PURPOSES			
; --------------------- END | MAIN PROCEDURE TO READ/WRITE FILE

; --------------------- BEGIN | PROCEDURE TO CLOSE FILE AND EXIT
SFPULA:		LD DE,FCB
			LD C,10H
			JP BDOS	
; --------------------- END | PROCEDURE TO CLOSE FILE AND EXIT			

; --------------------- BEGIN | PROCEDURE TO CLEAN FCB AREA
LIMPA:		LD HL,FCB
			LD DE,FCB+1
			LD BC,37
			XOR A
			LD (HL),A
			LDIR
			RET
; --------------------- END | PROCEDURE TO CLEAN FCB AREA

; --------------------- BEGIN | PROCEDURE TO READ SECTOR	
SETOR:		LD HL,DMADISK
			LD (ADDR),HL
			LD A,(NSET)
			LD (NST),A
			LD HL,(SETI)
			LD (SIT),HL
SLOOP:		LD B,4
			CALL CKESC
			LD HL,(ADDR)
			LD DE,(SIT)
			CALL PDADOS
			LD B,1
			LD A,(TCOPIA)
			LD C,A
			LD A,(OP)
			RRA
			LD A,(DRV)
			PUSH AF
			LD A,(ACESSO)
			AND A
			JR Z,SBIOS
			POP AF
			CALL RSETOR
			JR SEPUL
SBIOS:		POP AF
			CALL R144H
SEPUL:		LD HL,(ADDR)
			LD DE,200H
			ADD HL,DE
			LD (ADDR),HL
			LD HL,(SIT)
			INC HL
			LD (SIT),HL
			LD A,(NST)
			DEC A
			LD (NST),A
			JR NZ,SLOOP
			RET
; --------------------- END | PROCEDURE TO READ SECTOR			

; --------------------- END | PROCEDURE FIND DOS2
FINDDOS2:	LD HL,(INTERDISCO)
			PUSH HL
			LD A,(0F348H)
			AND 0FH
			LD B,A
			AND 3
			LD H,A
			LD A,B
			RRCA
			RRCA
			AND 3
			LD L,A
			LD (INTERDISCO),HL
			LD B,10
			CALL CHAVEIA
			POP HL
			LD (INTERDISCO),HL
			RET		
; --------------------- END | PROCEDURE FIND DOS2
			
; --------------------- BEGIN | PROCEDURE SET ACTUAL 16K MEMORY BLOCK
SETACTUALPAGE:
			LD A,(TMEMO)
			AND A
			RET NZ
			LD A,(ACTUALPAGE)
			OUT(0FDH),A
			RET
; --------------------- END | PROCEDURE SET ACTUAL 16K MEMORY BLOCK

; --------------------- BEGIN | PROCEDURE TO GET ONE MORE 16K MEMORY BLOCK
INCMEM:		LD A,(TMEMO)
			CP 1
			JR Z,IMMEG
			CP 2
			RET Z
			LD A,(PAGEM)
			OUT(0FDH),A
			LD (ACTUALPAGE),A
			INC A
			LD (PAGEM),A
			RET
IMMEG:		OUT(8EH),A
			LD A,(PAGEG)
			LD (4000H),A
			INC A
			LD (6000H),A
			INC A
			LD (PAGEG),A
			IN A,(8EH)
			RET 
; --------------------- EBD | PROCEDURE TO GET ONE MORE 16K MEMORY BLOCK			

; --------------------- BEGIN | PROCEDURE TO READ/WRITE 1 FISICAL SECTOR
RSETOR:		DI
			PUSH AF
			AND A
			JR Z,DRVA
			LD A,22H
			JR RSPULA
DRVA:		LD A,21H
RSPULA:		LD (DRIVE),A
			POP AF
			JR C,GRAV
			XOR A
			LD (READWRITE),A
			LD A,80H
			JR RSJP
GRAV:		LD A,1
			LD (READWRITE),A
			LD A,0A0H
RSJP:		LD (TOPER),A
			LD A,C
			CP 0FCH
			JR Z,SIMPLES
			CP 0F8H
			JR Z,SIMPLES
			LD A,2
			JR RSPUL
SIMPLES:	LD A,1
RSPUL:		LD (FACES),A
RSLOOP:		PUSH DE
			PUSH HL
			PUSH BC
			PUSH HL
			PUSH DE
			LD (BARRAVALDISK),DE
			LD HL,(BARRAVALFILE)
			ADD HL,DE
			INC HL
			INC HL
			LD (BARRAVALUE),HL
			CALL PROGRESSBAR
			POP DE
			POP HL
			CALL CSETOR
			LD (TRACKSECTOR),DE
			LD B,A
			LD A,(TATUAL)
			CP D
			JR Z,PDDRV
			LD A,B
			LD (LDATUAL),A
			CALL SETDRIVELADO
			LD A,D
			LD (TATUAL),A
			LD C,A
			LD B,5
			CALL CHAVEIA
			LD B,2
			CALL CHAVEIA
			JR PLDRV
PDDRV:		LD A,(LDATUAL)
			CP B
			JR Z,PLDRV
			LD A,B
			LD (LDATUAL),A
			CALL SETDRIVELADO
PLDRV:		LD A,D
			LD (CAB),A
			LD A,E
			LD C,A
			LD B,6
			CALL CHAVEIA
			LD A,(TOPER)
			CP 0A0H
			JR Z,MGRAVA
			LD B,7
			JR MPULA
MGRAVA:		LD B,8
MPULA:		CALL CHAVEIA
			POP BC
			POP HL
			LD DE,512
			ADD HL,DE
			POP DE
			INC DE
			DJNZ RSLOOP
			RET
; --------------------- END | PROCEDURE TO READ/WRITE 1 FISICAL SECTOR			

; --------------------- BEGIN | PROCEDURE TO SELECT INTERFACE	
SETINTER:	LD A,(OPERAC)
			AND A
			JR Z,SETINTERD1
			LD A,(DRIVE2)
			JR SETINTCONT
SETINTERD1:	LD A,(DRIVE1)
SETINTCONT:	CALL GETDRIVE
			LD (INTERFACE),A
			CP 0FFH
			JR Z,SETBIOS
			AND A
			JR NZ,SETINTFDC
SETBIOS:	XOR A
			LD (ACESSO),A
			JR SETINTPULA
SETINTFDC:	LD A,1
			LD (ACESSO),A
SETINTPULA:	LD A,H
			AND 0FH
			LD B,A
			AND 3
			LD D,A
			LD A,B
			AND 1100B
			RRCA
			RRCA
			LD E,A
			LD (INTERDISCO),DE
			LD A,H
			LD (INTERDISK),A	
			CALL SETREG
			RET
; --------------------- BEGIN | PROCEDURE TO SELECT INTERFACE	

; --------------------- BEGIN | PROCEDURE TO SELECT BDOS INTERFACE			
POWERPROC:
			LD HL,(INTERDISCO)
			LD DE,(DRIVELADO)
			LD A,(INTERFACE)
			LD B,A
			LD A,(INTERDISK)
			LD C,A
			PUSH HL
			PUSH DE
			PUSH BC
			LD A,(OPERAC)
			AND A
			JR Z,DESPOWERD2
			LD A,(DRIVE1)
			JR DESPOWERCNT
DESPOWERD2:	LD A,(DRIVE2)
DESPOWERCNT:CALL GETDRIVE
			PUSH AF
			LD A,H
			LD (INTERDISK),A
			POP AF
			CP 0FFH
			JR Z,OPCJUMP
			AND A
			JR Z,OPCJUMP
			LD (INTERFACE),A
			LD A,H
			AND 0FH
			LD B,A
			AND 3
			LD D,A
			LD A,B
			AND 1100B
			RRCA
			RRCA
			LD E,A
			LD (INTERDISCO),DE
			LD H,L
			INC H
			LD L,0
			LD (DRIVELADO),HL	
			CALL SETREG
			LD A,(POWERPROCOPC)
			CP 1
			JR Z,OPCDESLIGA
			LD B,12
			CALL CHAVEIA
			JR OPCJUMP
OPCDESLIGA:	LD B,3
			CALL CHAVEIA
OPCJUMP:	POP BC
			POP DE
			POP HL
			LD A,B
			LD (INTERFACE),A
			LD A,C
			LD (INTERDISK),A
			LD (INTERDISCO),HL
			LD (DRIVELADO),DE
			CALL SETREG
			RET		
; --------------------- END | PROCEDURE TO SELECT BDOS INTERFACE				
			
; --------------------- BEGIN | PROCEDURE TO TURN OFF DRIVE	
DESLIGADRIVE:
			PUSH BC
			LD B,3
			CALL CHAVEIA
			POP BC
			RET
; --------------------- END | PROCEDURE TO TURN OFF DRIVE		

; --------------------- BEGIN | PROCEDURE TO DELAY	
DELAY:		PUSH BC
			LD B,6
DELOOP:		EX (SP),HL
			EX (SP),HL
			DJNZ DELOOP
			POP BC
			RET
; --------------------- END | PROCEDURE TO DELAY				
		
; --------------------- BEGIN | PROCEDURES TO WAIT DEVICE IS READY					
WAIT:		IN A,(0D0H)
			RRA
			RET NC
			JR WAIT		
			
WAITRES:	LD HL,2
WAITRESBACK:LD DE,0FFFFH
WAITRESBAC2:LD A,E
			OR D
			JR Z,WAITSAI
			DEC DE
			IN A,(0D0H)
			RRA
			RET NC
			JR WAITRESBAC2	
WAITSAI:	DEC HL
			LD A,L
			OR H
			RET Z
			JR WAITRESBACK
			
; --------------------- BEGIN | PROCEDURES TO WAIT DEVICE IS READY	

; --------------------- BEGIN | PROCEDURE TO READ DATA USING MICROSOL FDC  		
READ:		LD DE,WRITEOUT
			PUSH DE
			LD A,80H
			OUT(0D0H),A
			CALL DELAY2
			LD C,0D3H
RRBACK:		IN A,(0D0H)
			RRA
			RET NC
			RRA
			JR NC,RRBACK
			INI
			JR RRBACK
; --------------------- END | PROCEDURE TO READ DATA USING MICROSOL FDC 			
			
; --------------------- BEGIN | PROCEDURE TO WRITE DATA USING MICROSOL FDC 			
WRITE:		LD DE,WRITEOUT
			PUSH DE
			LD A,0A0H
			OUT(0D0H),A
			CALL DELAY2			
			LD A,(0D0H)
			ADD A,A
			RET M
			LD C,0D3H
WRBACK:		IN A,(0D0H)
			RRA
			RET NC
			RRA
			JR NC,WRBACK
			OUTI
			JR WRBACK		
WRITEOUT:	IN A,(0D0H)
			PUSH AF
			CALL INTNOWCDX2
			POP AF
			RET

; --------------------- END | PROCEDURE TO WRITE DATA USING MICROSOL FDC 				
	
; --------------------- BEGIN | PROCEDURE TO GET FISICAL INFORMATION ABOUT SECTOR	
CSETOR:		PUSH HL
			PUSH BC
			PUSH DE
			PUSH DE
			POP BC  
			CALL DIV9
			PUSH BC
			POP HL
			LD A,1
			LD (LADO),A
			LD A,(DRIVE)
			LD (DRVBAK),A
			LD A,(FACES)
			AND 1
			JR NZ,CSF1
			BIT 0,C
			JR Z,CSPAR
			LD A,(DRIVE)
			SET 4,A
			LD (DRVBAK),A
			LD A,2
			LD (LADO),A
CSPAR:		SRL H
			RR L
CSF1:		LD A,L
			LD (TR),A
CALL 		MULTI9
			PUSH HL
			POP DE
			POP HL
			AND A
			SBC HL,DE
			INC HL
			LD E,L
			LD A,(TR)
			LD D,A
			LD A,(DRVBAK)
			POP BC
			POP HL
			RET
; --------------------- END | PROCEDURE TO GET FISICAL INFORMATION ABOUT SECTOR			
	
; --------------------- BEGIN | PROCEDURE TO RESTORE DRIVES	
RESTORE:	LD A,(DRIVE1)
			LD H,A
			LD A,(DRIVE2)
			LD L,A
			PUSH HL
			LD D,0
RESTLOOP:	PUSH DE
			LD A,D
			CALL GETDRIVE
			CP 0FFH
			JR Z,RESTPULA
			AND A
			JR Z,RESTPULA
			POP DE
			LD A,D
			LD (DRIVE1),A
			LD (DRIVE2),A
			PUSH DE
			CALL SETINTER
			LD HL,0100H
			LD (DRIVELADO),HL
			LD B,1
			CALL CHAVEIA
			LD HL,0200H
			LD (DRIVELADO),HL
			LD B,1
			CALL CHAVEIA
RESTPULA:	POP DE
			INC D
			LD A,D
			CP 8
			JR Z,RESTFIM
			JR RESTLOOP
RESTFIM:	XOR A
			LD (TATUAL1),A
			LD (TATUAL2),A
			LD (TATUAL),A
			POP HL
			LD A,H
			LD (DRIVE1),A
			LD A,L
			LD (DRIVE2),A
			CALL SETINTER
			RET
; --------------------- END | PROCEDURE TO RESTORE DRIVES

; --------------------- BEGIN | PROCEDURE TO TURN OFF DRIVE
DESDRIVELOK:LD HL,BUFFERADDR
			LD DE,0
			LD B,1
			XOR A
			JP R144H
; --------------------- END | PROCEDURE TO TURN OFF DRIVE

; --------------------- BEGIN | PROCEDURES TO PRINT WAIT MESSAGE
PRINTWAIT1:	CALL 6CH
			LD HL,2400H
			LD DE,800H
			LD BC,800H
			CALL ROTVRAM
			LD DE,MSGWAIT
			LD HL,457
			CALL PRINTH
			RET
			
PRINTWAIT2:	LD BC,2000
PRINTWAIT2L:CALL DELAY2
			DEC BC
			LD A,C
			OR B
			JR NZ,PRINTWAIT2L
			CALL 6CH
			LD HL,2400H
			LD DE,800H
			LD BC,800H
			CALL ROTVRAM
			CALL RTELA
			RET			
; --------------------- END | PROCEDURES TO PRINT WAIT MESSAGE
	
; --------------------- BEGIN | PROCEDURE TO CLEAR INFORMATION		
CLEARINFO:  LD DE,MSGCLEAR
			LD HL,654
			CALL PRINTH 
			LD DE,MSGCLEAR
			LD HL,694
			CALL PRINTH 
			LD DE,MSGCLEAR
			LD HL,734
			CALL PRINTH 
			RET
; --------------------- END | PROCEDURE TO CLEAR INFORMATION

; --------------------- BEGIN | PROCEDURE TO SET UP GUI INTERFACE		
GUI:		LD A,1
			LD (0FCABH),A
			LD A,40
			LD (0F3AEH),A
			LD A,15
			LD (0F3E9H),A
			LD (CFRENTE),A
			LD A,1
			LD (0F3EAH),A
			LD (CFUNDO),A
			CALL 6CH
			CALL 0CCH
			LD HL,BUFGUI
			LD DE,1000H
			LD BC,3C0H
			CALL 5CH				
			LD HL,BUFGUI+400H
			LD DE,800H
			LD BC,800H
			CALL 5CH
			LD HL,BUFGUI + 400H
			LD DE,2400H
			LD BC,800H
			CALL 5CH
			LD HL,BUFGUI + 0D00H
			LD DE,2000H
			LD BC,3C0H
			JP 5CH			
; --------------------- END | PROCEDURE TO SET UP GUI INTERFACE			
			
; --------------------- BEGIN | PROCEDURE TO SET UP MENU OPTIONS
MENU:		CALL 156H
			XOR A
			CALL CHGET
			CP 'D'
			JP Z,DISABLEMEM
			CP 'G'
			JP Z,ENABLEMRAM
			CP 'P'
			JP Z,ENABLEMAPPER
			CP '1'
			JP Z,ACFRENTE
			CP '2'
			JP Z,ACFUNDO
			CP 'H'
			JP Z,HELP
			CP 13
			RET Z
			CP 'N'
			JP Z,GNOME
			CP 'R'
			JR Z,TRESTORE
			CP 27
			JP NZ,MENPUL
			CALL 6CH
			POP HL
			JP FIMDOS
TRESTORE:	
			CALL RESTORE
			JP MENU			
; --------------------- END | PROCEDURE TO SET UP MENU OPTIONS

; --------------------- BEGIN | ROUTINES TO DISPLAY A WINDOW MESSAGE			
WINDOWCRC:	CALL STELAP
			LD HL,130DH
			LD DE,140EH
			LD BC,1005H
			LD IX,400H
			CALL JANELA 
			LD DE,SUMMSG
			LD HL,448
			CALL PRINTH			
			LD DE,(CHECKCALC)
			LD HL,546
			CALL PRINTHEX
			CALL CHGET
			CALL RTELAP
			RET	

WINDOWPROT:	CALL STELAP
			LD HL,130DH
			LD DE,140EH
			LD BC,1105H
			LD IX,400H
			CALL JANELA 
			LD DE,PROTMSG
			LD HL,486
			CALL PRINTH			
			CALL CHGET
			LD B,4
			CP 27
			JR Z,WINPROTSAI
			CALL RTELAP
			RET			
WINPROTSAI:	POP HL
			DJNZ WINPROTSAI	
			CALL RTELA
			JP COMECO			
; --------------------- END | ROUTINES TO DISPLAY A WINDOW MESSAGE				

; --------------------- BEGIN | ROUTINES TO ENABLE/DISABLE EXTRA MEMORIES		
OPENWINDOW:	CALL STELAP
			LD HL,130DH
			LD DE,140EH
			LD BC,1005H
			LD IX,400H
			CALL JANELA 
			LD DE,MSG_DEFAULT
			LD HL,487
			CALL PRINTH
			CALL CHGET
			CALL RTELAP
			RET
			
DISABLEMEM: CALL BLINK
			LD A,2
			LD (SELECTMEMO),A
			LD HL,MSG_NO_MEMORY
			LD DE,MSG_DEFAULT
			LD BC,27
			LDIR
			CALL OPENWINDOW
			JP MENU
			
ENABLEMRAM: CALL BLINK
			LD A,1
			LD (SELECTMEMO),A			
			LD HL,MSG_MEGARAM
			LD DE,MSG_DEFAULT
			LD BC,27
			LDIR
			CALL OPENWINDOW
			JP MENU
			
ENABLEMAPPER:
			CALL BLINK
			XOR A
			LD (SELECTMEMO),A
			LD HL,MSG_MAPPER
			LD DE,MSG_DEFAULT
			LD BC,27
			LDIR
			CALL OPENWINDOW
			JP MENU
; --------------------- END | ROUTINES TO ENABLE/DISABLE EXTRA MEMORIES	
		
; --------------------- BEGIN | ROUTINE TO BLINK SCREEN	
BLINK:		DI
			LD A,1FH
			OUT(099H),A
			LD A,7
			OR 80H
			OUT(099H),A
			CALL BLINKDELAY
			LD A,0F1H
			OUT(099H),A
			LD A,7
			OR 80H
			OUT(099H),A
			EI	
			LD A,15
			LD (CFRENTE),A
			LD A,1
			LD (CFUNDO),A			
			RET
BLINKDELAY:	LD BC,10FFH
BLINKBACK:	EX (SP),HL
			EX (SP),HL
			DEC C
			JR NZ,BLINKBACK
			DJNZ BLINKBACK
			RET
; --------------------- END | ROUTINE TO BLINK SCREEN	
		
; --------------------- BEGIN | ROUTINE TO ALTER THE FRONT COLOR		
ACFRENTE:	LD A,(CFRENTE)
			INC A
			AND 0FH
			LD (CFRENTE),A
			LD (0F3E9H),A
			CALL 62H
			JP MENU
; --------------------- END | ROUTINE TO ALTER THE FRONT COLOR		
; --------------------- BEGIN | ROUTINE TO ALTER THE BACKGROUND COLOR			
ACFUNDO:	LD A,(CFUNDO)
			INC A
			AND 0FH
			LD (CFUNDO),A
			LD (0F3EAH),A
			CALL 62H
			JP MENU
; --------------------- END | ROUTINE TO ALTER THE BACKGROUND COLOR
; --------------------- BEGIN | ROUTINE TO DISPLAY THE HELP WINDOW	
HELP:		CALL STELAP
			LD HL,130BH
			LD DE,140CH
			LD BC,120BH
			LD IX,50H
			CALL JANELA 
			LD DE,MSGHELP
			LD HL,124
			CALL PRINTH
			CALL CHGET
			CALL RTELAP
			JP MENU		
; --------------------- END | ROUTINE TO DISPLAY THE HELP WINDOW		
; --------------------- BEGIN | ROUTINE TO SET UP FILE NAME	
GNOME:		CALL LBARRA
			CALL GETNOME
			CALL PBARRA
			JP MENU
; --------------------- END | ROUTINE TO SET UP FILE NAME	
; --------------------- BEGIN | ROUTINE TO SET MENU OPTIONS
MENPUL:		PUSH AF
			LD A,(ATUAL)
			CP 1
			JP Z,MENU1
			CP 2
			JP Z,MENU2
			CP 3
			JP Z,MENU3
			CP 4
			JP Z,MENU4
			CP 5
			JP Z,MENU5
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 6
			POP AF
			CP 30
			JR Z,MN630
			CP 31
			JR Z,MN631
			CALL OPC6
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 6
MN630:		CALL BARRA5
			LD A,5
			LD (ATUAL),A
			JP MENU
MN631:		CALL BARRA1
			LD A,1
			LD (ATUAL),A
			JP MENU
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 1
MENU1:		POP AF
			CP 30
			JR Z,MN130
			CP 31
			JR Z,MN131
			CALL OPC1
			JP MENU
MN130:		CALL BARRA6
			LD A,6
			LD (ATUAL),A
			JP MENU
MN131:		CALL BARRA2
			LD A,2
			LD (ATUAL),A
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 1
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 2
MENU2:		POP AF
			CP 30
			JR Z,MN230
			CP 31
			JR Z,MN231
			CALL OPC2
			JP MENU
MN230:		CALL BARRA1
			LD A,1
			LD (ATUAL),A
			JP MENU
MN231:		CALL BARRA3
			LD A,3
			LD (ATUAL),A
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 2
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 3
MENU3:		POP AF
			CP 30
			JR Z,MN330
			CP 31
			JR Z,MN331
			CALL OPC3
			JP MENU
MN330:		CALL BARRA2
			LD A,2
			LD (ATUAL),A
			JP MENU
MN331:		CALL BARRA4
			LD A,4
			LD (ATUAL),A
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 3
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 4
MENU4:		POP AF
			CP 30
			JR Z,MN430
			CP 31
			JR Z,MN431
			CALL OPC4
			JP MENU
MN430:		CALL BARRA3
			LD A,3
			LD (ATUAL),A
			JP MENU
MN431:		CALL BARRA5
			LD A,5
			LD (ATUAL),A
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 4
; --------------------- BEGIN | ROUTINE TO SET UP MENU OPTION 5
MENU5:		POP AF
			CP 30
			JR Z,MN530
			CP 31
			JR Z,MN531
			CALL OPC5
			JP MENU
MN530:		CALL BARRA4
			LD A,4
			LD (ATUAL),A
			JP MENU
MN531:		CALL BARRA6
			LD A,6
			LD (ATUAL),A
			JP MENU
; --------------------- END | ROUTINE TO SET UP MENU OPTION 5			
; --------------------- END | ROUTINE TO SET MENU OPTIONS	

; --------------------- BEGIN | PROCEDURE TO CLEAN BARS
LBARRA:		LD DE,BAR1
			LD HL,245
			CALL PRINTH
			LD DE,BAR2
			LD HL,285
			CALL PRINTH
			LD DE,BAR3
			LD HL,325
			CALL PRINTH
			LD DE,BAR4
			LD HL,365
			CALL PRINTH
			LD DE,BAR5
			LD HL,405
			CALL PRINTH
			LD DE,BAR6
			LD HL,445
			CALL PRINTH
			LD DE,BAR7
			LD HL,485
			JP PRINTH			
; --------------------- END | PROCEDURE TO CLEAN BARS			
			
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 1			
BARRA1:		CALL LBARRA
			LD HL,BAR1
			LD DE,245
			LD BC,14
			JP WVBINV
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 1			
			
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 2			
BARRA2:		CALL LBARRA
			LD HL,BAR2
			LD DE,285
			LD BC,14
			JP WVBINV
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 2		
			
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 3		
BARRA3:		CALL LBARRA
			LD HL,BAR3
			LD DE,325
			LD BC,14
			JP WVBINV
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 3		
			
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 4			
BARRA4:		CALL LBARRA
			LD HL,BAR4
			LD DE,365
			LD BC,14
			JP WVBINV
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 4		
			
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 5			
BARRA5:		CALL LBARRA
			LD HL,BAR5
			LD DE,405
			LD BC,14
			JP WVBINV
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 5

; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN MENU OPTION 6	
BARRA6:		CALL LBARRA
			LD HL,BAR6
			LD DE,445
			LD BC,14
			JP WVBINV  
; --------------------- END | PROCEDURE TO DRAW A BAR IN MENU OPTION 6		

; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 1
OPC1:		PUSH AF
			LD A,(TCOPIA)
			CP 0FCH
			JR Z,OPC1FC
			CP 0FDH
			JR Z,OPC1FD
			CP 0F8H
			JP Z,OPC1F8
			POP AF
			CP 28
			JR Z,O1F928
			CP 29
			JR Z,O1F929
			RET
O1F929:		LD DE,MSG1C
			LD HL,262
			CALL PRINTH	
			LD A,0F8H
			LD (TCOPIA),A
			RET
O1F928:		LD DE,MSG1A
			LD HL,262
			CALL PRINTH		
			LD A,0FCH
			LD (TCOPIA),A
			RET
OPC1FC:		POP AF
			CP 28
			JR Z,O1FC28
			CP 29
			JR Z,O1FC29
			RET
O1FC29:		LD DE,MSG1D
			LD HL,262
			CALL PRINTH			
			LD A,0F9H
			LD (TCOPIA),A
			RET
O1FC28:		LD DE,MSG1B
			LD HL,262
			CALL PRINTH			
			LD A,0FDH
			LD (TCOPIA),A
			RET
OPC1FD:		POP AF
			CP 28
			JR Z,O1FD28
			CP 29
			JR Z,O1FD29
			RET
O1FD29:		LD DE,MSG1A
			LD HL,262
			CALL PRINTH
			LD A,0FCH
			LD (TCOPIA),A
			RET
O1FD28:		LD DE,MSG1C
			LD HL,262
			CALL PRINTH
			LD A,0F8H
			LD (TCOPIA),A
			RET
OPC1F8:		POP AF
			CP 28
			JR Z,O1F828
			CP 29
			JR Z,O1F829
			RET
O1F829:		LD DE,MSG1B
			LD HL,262
			CALL PRINTH
			LD A,0FDH
			LD (TCOPIA),A
			RET
O1F828:		LD DE,MSG1D
			LD HL,262
			CALL PRINTH
			LD A,0F9H
			LD (TCOPIA),A
			RET
; --------------------- END | PROCEDURE TO SET UP VALUES IN MENU OPTION 1	

; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 2
OPC2:		PUSH AF
			LD A,(OPERAC)
			AND A
			JR Z,OPC20
			POP AF
			CP 28
			JR Z,OP21
			CP 29
			JR Z,OP21
			RET
OP21:		LD DE,MSG2A
			LD HL,302
			CALL PRINTH
			XOR A
			LD (OPERAC),A
			CALL SETINTER
			CALL OP5B
			;CALL ARRUMADRIVE
			RET 
OPC20:		POP AF
			CP 28
			JR Z,OP20
			CP 29
			JR Z,OP20
			RET
OP20:		LD DE,MSG2B
			LD HL,302
			CALL PRINTH
			LD A,1
			LD (OPERAC),A
			CALL SETINTER
			CALL OP5B
			;CALL ARRUMADRIVE
			RET
; --------------------- END | PROCEDURE TO SET UP VALUES IN MENU OPTION 2

; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 3
OPC3:		PUSH AF
			LD HL,342
			LD DE,DRIVE1
OPCBAK:		LD A,(DE)
			LD B,A
			POP AF
			CP 28
			JR Z,OPC328
			CP 29
			JR Z,OPC329
			RET
OPC328:		CALL OPCKP
OPC3BK:		LD (DE),A
			ADD A,41H
			CALL WVBYTE
			CALL SETINTER
			CALL OP5B
			RET
OPC329:		CALL OPCKM
			JR OPC3BK
; --------------------- END | PROCEDURE TO SET UP VALUES IN MENU OPTION 3			

; --------------------- BEGIN | PROCEDURES TO SET MAX VALUE OF DRIVE
OPCCKMAX:	PUSH AF
			LD A,(ACESSO)
			AND A
			JR NZ,MAXCONT
			POP AF
			RET
MAXCONT:	LD A,(OPERAC)
			AND A
			JR Z,MAXDF
			LD A,L
			CP 97H
			JR Z,MAX97
			POP AF
			RET
MAX97:		POP AF
			CP 2
			JR Z,MAX2
			RET
MAX2:		XOR A
			RET
MAXDF:		LD A,L
			CP 6FH
			JR Z,MAX6F
			POP AF
			RET
MAX6F:		POP AF
			CP 2
			JR Z,MAX2
			RET
			
OPCCKMIN:	PUSH AF
			LD A,(ACESSO)
			AND A
			JR NZ,MINCONT
			POP AF
			RET
MINCONT:	LD A,(OPERAC)
			AND A
			JR Z,MINDF
			LD A,L
			CP 97H
			JR Z,MIN97
			POP AF
			RET
MIN97:		POP AF
			CP 7
			JR Z,MIN7
			RET
MIN7:		LD A,1
			RET
MINDF:		LD A,L
			CP 6FH
			JR Z,MIN6F
			POP AF
			RET
MIN6F:		POP AF
			CP 7
			JR Z,MIN7
			RET				
; --------------------- END | PROCEDURES TO SET MAX VALUE OF DRIVE

; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 4
OPC4:		PUSH AF
			LD HL,382
			LD DE,DRIVE2
			JP OPCBAK
OPCKP:		LD A,B
			CP 7
			JR Z,OPCKP7
			INC A
			;CALL OPCCKMAX
			RET
OPCKP7:		XOR A
			RET
OPCKM:		LD A,B
			AND A
			JR Z,OPCKM0
			DEC A
			RET
OPCKM0:		LD A,7
			;CALL OPCCKMIN
			RET
; --------------------- END | PROCEDURE TO SET UP VALUES IN MENU OPTION 4

; --------------------- BEGIN | PROCEDURE TO SET DRIVES TO A
ARRUMADRIVE:LD A,(ACESSO)
			AND A
			RET Z
			LD HL,623
			LD A,(OPERAC)
			AND A
			JR NZ,ARRUMA2
			LD DE,DRIVE1
			JP ARRUMACONT
ARRUMA2:	LD HL,663
			LD DE,DRIVE2
ARRUMACONT:	LD A,(DE)
			AND A
			RET Z
			CP 1
			RET Z
			LD A,0
			LD (DE),A
			ADD A,41H
			JP WVBYTE
; --------------------- BEGIN | PROCEDURE TO SET DRIVES TO A
			
; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 5	
OPC5:		PUSH AF
			LD A,(ACESSO)
			AND A
			JR Z,OPC5B
			POP AF
			CP 28
			JR Z,OP5D
			CP 29
			JR Z,OP5D
			RET
OP5D:		LD DE,MSG5A
			LD HL,422
			CALL PRINTH
			XOR A
			LD (ACESSO),A
			;CALL ARRUMADRIVE
			RET
OPC5B:		POP AF
			CP 28
			JR Z,OP5B
			CP 29
			JR Z,OP5B
			RET
OP5B:		LD A,(INTERFACE)
			AND A
			JR Z,OP5D
			CP 0FFH
			JR Z,OP5D
			CP 1
			JR Z,OP5B1
			CP 2
			JR Z,OP5B2
			CP 3
			JR Z,OP5B3
			LD DE,MSG5B4
			JR OP5BC	
OP5B1:		LD DE,MSG5B1
			JR OP5BC
OP5B2:		LD DE,MSG5B2
			JR OP5BC
OP5B3:		LD DE,MSG5B3			
OP5BC:		LD HL,422
			CALL PRINTH 
			LD A,1
			LD (ACESSO),A
			;CALL ARRUMADRIVE
			RET
; --------------------- END | PROCEDURE TO SET UP VALUES IN MENU OPTION 5

; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 6	
OPC6:		PUSH AF
			LD A,(OVER)
			AND A
			JR Z,OPC6N
			POP AF
			CP 28
			JR Z,OP6E
			CP 29
			JR Z,OP6E
			RET
OP6E:		LD DE,MSG6A
			LD HL,462
			CALL PRINTH
			XOR A
			LD (OVER),A
			RET
OPC6N:		POP AF
			CP 28
			JR Z,OP6N
			CP 29
			JR Z,OP6N
			RET
OP6N:		LD DE,MSG6B
			LD HL,462
			CALL PRINTH
			LD A,1
			LD (OVER),A
			RET	
; --------------------- BEGIN | PROCEDURE TO SET UP VALUES IN MENU OPTION 6

; --------------------- BEGIN | PROCEDURE TO PRINT A NUMBER IN DECIMAL (4 DIGITS)	
PRINT:		PUSH AF
			PUSH BC
			LD (PNUM),HL
			LD (PEND),DE
			LD B,0
PNTL1:		LD DE,1000
			XOR A
			SBC HL,DE
			JR C,PNTC1
			LD (PNUM),HL 
			INC B
			JR PNTL1
PNTC1:		LD HL,(PEND)
			PUSH HL
			LD A,B
			ADD A,30H
			CALL WVBYTE
			POP HL
			INC HL
			LD (PEND),HL
			LD HL,(PNUM)
			LD  B,0
PNTL2:		LD DE,100
			XOR A
			SBC HL,DE
			JR C,PNTC2
			LD (PNUM),HL
			INC B
			JR PNTL2
PNTC2:		LD HL,(PEND)
			PUSH HL
			LD A,B
			ADD A,30H
			CALL WVBYTE
			POP HL
			INC HL
			LD (PEND),HL
			LD HL,(PNUM)
			LD B,0
PNTL3:		LD DE,10
			XOR A
			SBC HL,DE
			JR C,PNTC3
			LD (PNUM),HL
			INC B
			JR PNTL3
PNTC3:		LD HL,(PEND)
			PUSH HL
			LD A,B
			ADD A,30H
			CALL WVBYTE
			POP HL
			INC HL
			LD DE,(PNUM)
			LD A,E
			ADD A,30H
			CALL WVBYTE
			POP BC
			POP AF
			RET  
; --------------------- END | PROCEDURE TO PRINT A NUMBER IN DECIMAL (4 DIGITS)				

; --------------------- BEGIN | PROCEDURE TO PRINT A NUMBER IN DECIMAL (2 DIGITS)		
PRINT2:		PUSH AF
			PUSH BC
			LD (PNUM),HL
			LD (PEND),DE
			LD B,0
P2L1:		LD DE,10
			XOR A
			SBC HL,DE
			JR C,P2C1
			LD (PNUM),HL
			INC B
			JR P2L1
P2C1:		LD HL,(PEND)
			PUSH HL
			LD A,B
			ADD A,30H
			CALL WVBYTE
			POP HL
			INC HL
			LD DE,(PNUM)
			LD A,E
			ADD A,30H
			CALL WVBYTE
			POP BC
			POP AF
			RET    
; --------------------- END | PROCEDURE TO PRINT A NUMBER IN DECIMAL (2 DIGITS)

; --------------------- BEGIN | PROCEDURE TO SHOW TRACK/SECTOR/SIDE INFORMATION	
PDADOS:		PUSH HL
			PUSH BC
			PUSH DE
			PUSH DE
			POP HL
			LD DE,863
			PUSH HL
			CALL PRINT
			POP DE
			LD A,(TCOPIA)
			CP 0FCH
			JR Z,PDSI
			CP 0F8H
			JR Z,PDSI
			LD A,2
			JR PDPUL
PDSI:		LD A,1
PDPUL:		LD (FACES),A
			LD A,21H
			LD (DRIVE),A
			CALL CSETOR
			LD HL,0
			LD A,D
			LD L,A
			LD DE,850
			CALL PRINT2
			LD A,(LADO)
			LD HL,0
			LD L,A
			LD DE,876
			CALL PRINT2
			POP DE
			POP BC
			POP HL
			RET 
; --------------------- END | PROCEDURE TO SHOW TRACK/SECTOR/SIDE INFORMATION	
	
; --------------------- BEGIN | PROCEDURES TO SAVE SCREEN INFORMATION IN VRAM
STELA:		LD HL,0
			LD DE,1000H
			LD BC,3C0H
			JP ROTVRAM
			
STELAP:		LD HL,0
			LD DE,1400H
			LD BC,3C0H
			JP ROTVRAM
; --------------------- END | PROCEDURE TO SAVE SCREEN INFORMATION IN VRAM 
		
; --------------------- BEGIN | PROCEDURES TO RESTORE SCREEN INFORMATION FROM VRAM 		
RTELA:		LD HL,1000H
			LD DE,0
			LD BC,3C0H
			JP ROTVRAM
			
RTELA2:		LD A,(QTDINTERFACES)
			CP 1
			RET Z
			LD HL,2000H
			LD DE,0
			LD BC,3C0H
			JP ROTVRAM			
			
RTELAP:		LD HL,1400H
			LD DE,0
			LD BC,3C0H
			JP ROTVRAM
; --------------------- END | PROCEDURE TO RESTORE SCREEN INFORMATION FROM VRAM 
			
; --------------------- BEGIN | PROCEDURE TO READ/WRITE BYTES IN VRAM 			
ROTVRAM:	PUSH HL
			PUSH BC
			PUSH DE
			CALL 4AH
			POP HL
			PUSH HL
			CALL WVBYTE
			POP DE
			POP BC
			POP HL
			INC HL
			INC DE
			DEC BC
			LD A,C
			OR B
			JR NZ,ROTVRAM
			RET
; --------------------- END | PROCEDURE TO READ/WRITE BYTES IN VRAM			

; --------------------- BEGIN | PROCEDURE TO SHOW STOP MESSAGE IF ONLY 1 DRIVE
LER:		LD A,(DRIVE1)
			LD B,A
			LD A,(DRIVE2)
			CP B
			RET NZ
			CALL CLEARINFO
			LD DE,MSGO1
			LD HL,654
			CALL PRINTH	
			CALL CHGET
			PUSH AF
			LD DE,MSGCLEAR
			LD HL,654
			CALL PRINTH
			POP AF
			CP 27
			RET NZ
			POP HL
			POP HL
			CALL RTELA
			JP COMECO
; --------------------- END | PROCEDURE TO SHOW STOP MESSAGE IF ONLY 1 DRIVE

; --------------------- BEGIN | PROCEDURE TO SHOW STOP MESSAGE IF ONLY 1 DRIVE
GRAVAR:		LD A,(DRIVE1)
			LD B,A
			LD A,(DRIVE2)
			CP B
			RET NZ
			CALL CLEARINFO
			LD DE,MSGD
			LD HL,654
			CALL PRINTH	
			CALL CHGET
			PUSH AF
			LD DE,MSGCLEAR
			LD HL,654
			CALL PRINTH
			POP AF			
			CP 27
			RET NZ
			POP HL
			POP HL
			CALL RTELA
			JP COMECO
; --------------------- END | PROCEDURE TO SHOW STOP MESSAGE IF ONLY 1 DRIVE			

; --------------------- BEGIN | PROCEDURE TO SHOW READING FILE MESSAGE	
LENDOA:		LD DE,MSGLA
			LD HL,694
			JP PRINTH
; --------------------- END | PROCEDURE TO SHOW READING FILE MESSAGE			
	
; --------------------- BEGIN | PROCEDURE TO SHOW READING SECTORS MESSAGE		
LENDOS:		LD DE,MSGLS
			LD HL,694
			JP PRINTH
; --------------------- END | PROCEDURE TO SHOW READING SECTORS MESSAGE			

; --------------------- BEGIN | PROCEDURE TO SHOW SAVING FILE MESSAGE		
GRAA:		LD DE,MSGGA
			LD HL,694
			JP PRINTH
; --------------------- END | PROCEDURE TO SHOW SAVING FILE MESSAGE			
	
; --------------------- BEGIN | PROCEDURE TO SHOW SAVING SECTORS MESSAGE		
GRAS:		LD DE,MSGGS
			LD HL,694
			JP PRINTH
; --------------------- END | PROCEDURE TO SHOW SAVING SECTORS MESSAGE			
	
; --------------------- BEGIN | PROCEDURE TO SET UP FILE NAME		
GETNOME:	CALL LNOME
			LD HL,170DH
			LD (CURSOR),HL
			LD DE,502
			LD HL,502
			LD (ENDER),DE 
			CALL CHGCUR
GNLOOP:		CALL CHGET
			CP 8
			JR Z,BS
			CP 13
			JR Z,GNSAI
			PUSH AF
			LD HL,(CURSOR)
			LD A,H
			CP 1FH
			JR NZ,GNPUL
			POP AF
			JR GNLOOP
GNPUL:		INC H
			LD (CURSOR),HL
			LD HL,(ENDER)
			POP AF
			PUSH HL
			CALL WVBYTE
			POP HL
			INC HL
			CALL CHGCUR
			LD (ENDER),HL
			JP GNLOOP
BS:			LD HL,(CURSOR)
			LD A,H
			CP 17H
			JP Z,GNLOOP
			DEC H
			LD (CURSOR),HL
			LD HL,(ENDER)
			DEC HL
			LD (ENDER),HL
			LD A,'_'
			PUSH HL
			CALL WVBYTE
			POP HL
			CALL CHGCUR
			INC HL
			PUSH HL
			LD HL,(CURSOR)
			LD A,H
			POP HL
			CP 1EH
			JR NZ,BSN18
			LD A,'.'
			JR BSCON
BSN18:		LD A,'_'
BSCON:		CALL WVBYTE   
			JP GNLOOP   
GNSAI:		LD HL,(CURSOR)
			LD A,H
			LD HL,(ENDER)
			CP 1FH
			JR NZ,SAIN19
			LD A,'.'
			JR SAICON
SAIN19:		LD A,'_'
SAICON:		CALL WVBYTE
			LD HL,502
			LD DE,NOME+1
			LD BC,8
			CALL 59H
			LD HL,NOME+1
			LD B,8
GNSL:		LD A,(HL)
			CP '_'
			JR NZ,GNN
			LD A,20H
			LD (HL),A
GNN:		INC HL
			DJNZ GNSL
			RET 
; --------------------- END | PROCEDURE TO SET UP FILE NAME	

; --------------------- BEGIN | PROCEDURE TO ERASE FILE NAME		
LNOME:		LD HL,502
			LD B,8
LNLOOP:		PUSH HL
			PUSH BC
			LD A,'_'
			CALL WVBYTE
			POP BC
			POP HL
			INC HL
			DJNZ LNLOOP
			LD A,'.'
			CALL WVBYTE
			RET
; --------------------- END | PROCEDURE TO ERASE FILE NAME
	
; --------------------- BEGIN | PROCEDURE TO PRINT BYTES LOADED/SAVED
PPART:		LD A,(OPERAC)
			AND A
			JR Z,PSAVED
			LD DE,MSGPAR
			LD HL,741
			JR PCONT
PSAVED:		LD DE,MSGPAR2
			LD HL,741
			;CALL WVM
PCONT:		CALL PRINTH
			LD HL,(BLOCKI)
			INC HL
			LD DE,734
			CALL PRINTSIX
			RET
; --------------------- END | PROCEDURE TO PRINT BYTES LOADED/SAVED			
	
; --------------------- BEGIN | PROCEDURE TO IDENTIFY AND USE MEMORY EXPANSIONS
CHKMEM:		LD HL,0
			LD (TPMEG),HL
			LD (TPMAP),HL
			JP CHKPULA
SLOT:DW 0
STMEG:DW 0
STMAP:DW 0
VSLOTP:DB 0
VSLOTS:DB 0
VSLOTE:DB 0
TPMAP:DW 0
TPMEG:DW 0
CHKPULA:	CALL ZSLOT
			CALL GETSLOT
			LD HL,0
			LD (SLOT),HL
CHKLOOP:	LD HL,(SLOT)
			CALL SETSLOT
			CALL VERSLOT
			CALL CHKFIM
			JR NC,CHKLOOP
			JP SLOTFIM
; --------------------- BEGIN | PROCEDURE TO IDENTIFY AND USE MEMORY EXPANSIONS		
	
; --------------------- BEGIN | PROCEDURE TO INICIALIZE SLOT SEARCH FOR MEMORY EXPANSIONS
ZSLOT:		LD HL,0
			LD (STMAP),HL
			LD (STMEG),HL
			LD (TPMEG),HL
			LD (TPMAP),HL			
			LD IX,0
			LD IY,0
			RET   
; --------------------- END | PROCEDURE TO INICIALIZE SLOT SEARCH FOR MEMORY EXPANSIONS
	
; --------------------- BEGIN | PROCEDURE TO GET SLOT TO BE USED
GETSLOT:	CALL 138H
			AND 00001100B
			RRCA
			RRCA
			LD (VSLOTP),A
			LD DE,0
			LD E,A
			LD HL,0FCC1H
			ADD HL,DE
			LD A,(HL)
			LD (VSLOTE),A
			LD DE,4
			ADD HL,DE
			LD A,(HL)
			AND 00001100B
			RRCA
			RRCA
			LD (VSLOTS),A 
			RET
; --------------------- END | PROCEDURE TO GET SLOT TO BE USED			

; --------------------- BEGIN | PROCEDURE TO SET SLOT IN PAGE 1
SETSLOT:	LD B,80H
			LD A,L
			RLCA
			RLCA  
			OR B
			LD B,A
			LD A,H
			OR B
			LD HL,4000H
			JP 24H		
; --------------------- END | PROCEDURE TO SET SLOT IN PAGE 1
	
; --------------------- BEGIN | PROCEDURE TO RESTORE ORIGINAL SLOT FROM PAGE 1	
SLOTFIM:	LD A,(VSLOTE)
			LD B,A
			LD A,(VSLOTS)
			RLCA
			RLCA
			OR B
			LD B,A
			LD A,(VSLOTP)
			OR B
			LD HL,4000H
			CALL 24H
			LD HL,(STMAP)
			LD DE,(STMEG)
			LD IX,(TPMAP)
			LD IY,(TPMEG)
			RET
; --------------------- END | PROCEDURE TO RESTORE ORIGINAL SLOT FROM PAGE 1				
	
; --------------------- BEGIN | PROCEDURE TO CHECK END OF SEARCH		
CHKFIM:		LD HL,(SLOT)
			LD A,L
			CP 3
			JR Z,CHKFL3
			INC L
			LD (SLOT),HL
			XOR A
			RET
CHKFL3:		LD A,H
			CP 3
			JR Z,CHKFH3
			LD L,0
			INC H
			LD (SLOT),HL
			XOR A
			RET
CHKFH3:		SCF
			RET
; --------------------- END | PROCEDURE TO CHECK END OF SEARCH				
	
; --------------------- BEGIN | PROCEDURE TO VERIFY MEMORY TYPE IN THE SLOT		
VERSLOT:	LD HL,4000H
			OUT(8EH),A
			LD A,(HL)
			INC A  
			LD (HL),A
			LD B,A
			LD A,(HL)
			CP B
			JR Z,VSRAM
			IN A,(8EH)
			LD A,(HL)
			INC A
			LD (HL),A
			LD B,A
			LD A,(HL)
			CP B
			RET NZ
			LD HL,(SLOT)
			CALL VSEXP
			LD (STMEG),HL
			JP CPMEG
VSRAM:		LD A,2
			OUT(0FDH),A
			XOR A
			LD (HL),A
			LD A,3
			OUT(0FDH),A
			LD A,1
			LD (HL),A
			LD A,2
			OUT(0FDH),A
			LD A,(HL)
			AND A
			RET NZ
			LD HL,(SLOT)
			LD (STMAPTEMP),HL
CPMAP:		LD B,0
			LD HL,4000H
MALOOP:		LD A,B
			OUT(0FDH),A
			XOR A
			LD (HL),A
			LD A,B
			INC B
			CP 0FFH
			JR NZ,MALOOP
			LD B,0
			LD DE,0
MA2LOOP:	LD A,B
			OUT(0FDH),A
			LD A,(HL)
			AND A
			JR NZ,MA2SAI
			LD A,1
			LD (HL),A
			INC B
			INC DE
			JR MA2LOOP
MA2SAI:		DEC DE
			DEC DE
			DEC DE
			DEC DE
			LD A,E
			OR D
			JR NZ,MA2S
			JR CHECKCONT
MA2S:		CALL CHECKMAIOR				
CHECKCONT:	PUSH DE
			POP IX
			LD A,2
			OUT(0FDH),A
			RET
CHECKMAIOR: PUSH DE
			LD HL,(TPMAP)
CHECKMLOOP: LD A,L
			OR H
			JR Z,CHECKMDE
			LD A,E
			OR D
			JR Z,CHECKMHL
			DEC HL
			DEC DE
			JR CHECKMLOOP
CHECKMDE:	POP DE
			LD (TPMAP),DE
			LD HL,(STMAPTEMP)
			LD (STMAP),HL
			RET
CHECKMHL:	POP DE
			RET
CPMEG:		LD B,0
			LD HL,4000H
MELOOP:		OUT(8EH),A
			LD A,B 
			LD (HL),A
			IN A,(8EH)
			XOR A
			LD (HL),A
			LD A,B
			INC B
			CP 0FFH
			JR NZ,MELOOP
			LD B,0
			LD DE,0
ME2LOOP:	OUT(8EH),A
			LD A,B
			LD (HL),A
			IN A,(8EH)
			LD A,(HL)
			AND A
			JR NZ,ME2SAI
			LD A,1
			LD (HL),A
			INC B
			INC DE
			JR ME2LOOP
ME2SAI:		LD (TPMEG),DE
			PUSH DE
			POP IY
			RET
; --------------------- END | PROCEDURE TO VERIFY MEMORY TYPE IN THE SLOT	
	
; --------------------- BEGIN | PROCEDURE TO VERIFY IF SLOT IS EXPANDED	
VSEXP:		PUSH HL
			LD A,H
			LD DE,0
			LD E,A
			LD HL,0FCC1H
			ADD HL,DE
			LD A,(HL)
			CP 80H
			JR NZ,VSESIM
			POP HL
			RET
VSESIM:		POP HL
			LD L,0
			RET  
; --------------------- END | PROCEDURE TO VERIFY IF SLOT IS EXPANDED	
				
; --------------------- BEGIN | PROCEDURE TO SET DEFAULT MEMORY CONFIGURATIONS	
FIM:		IN A,(0A8H)
			AND 0F0H
			OUT(0A8H),A
FIMMAP:		LD A,(TMEMO)
			AND A
			RET NZ
			LD A,2
			OUT(0FDH),A
			RET 
; --------------------- END | PROCEDURE TO SET DEFAULT MEMORY CONFIGURATIONS				
	
; --------------------- BEGIN | PROCEDURES TO DEFINE MEMORY TYPE TO BE USED	
MANUALMRAM:	LD A,E
			OR D
			JR NZ,SMMEG
			JR SMNORM
MANUALMAPPER:
			LD A,L
			OR H
			JR Z,SMNORM	
			XOR A
			LD (TMEMO),A
			JR SMP2		
MANUALRAM:	LD A,L
			OR H
			JR Z,SMNORM
			LD DE,1
			LD (TPMAP),DE
			LD A,2
			LD (TMEMO),A
			JR SMP2			
			
SETMEMO:	LD (SLOTMAPPER),HL
			LD (SLOTMEGARAM),DE
			LD A,(SELECTMEMO)
			CP 2
			JR Z,MANUALRAM
			CP 1
			JR Z,MANUALMRAM
			AND A
			JR Z,MANUALMAPPER
			LD A,L
			OR H
			JR NZ,SMMAP
SETMEMO2:	LD HL,(SLOTMAPPER)
			LD DE,(SLOTMEGARAM)
			LD A,E
			OR D
			JR NZ,SMMEG
SMNORM:		LD HL,1
			LD (TPAGES),HL
			LD A,2
			LD (TMEMO),A
			CALL SETRAM
			RET
SMMAP:		CALL CKMAIOR
			JR NZ,SETMEMO2
SMP2:		LD B,80H
			LD A,H
			OR B
			LD B,A
			LD A,L
			RLCA
			RLCA
			OR B
			LD HL,4000H
			CALL 0024H
			LD HL,(TPMAP)
			LD (TPAGES),HL
			RET
SMMEG:		PUSH DE
			LD HL,(TPMEG)
			CALL DIV2
			LD (TPMAP),HL
			LD A,1
			LD (TMEMO),A
			POP HL
			JR SMP2
			
CKMAIOR:	PUSH DE
			PUSH HL
			LD HL,(TPMEG)
			CALL DIV2
			PUSH HL
			POP DE
			LD HL,(TPMAP)
			XOR A
			SBC HL,DE
			JR C,MEGMAI
			LD A,(DOS2)
			AND A
			JR NZ,CKMAIORPOP
			XOR A
			LD (TMEMO),A
			POP HL
			POP DE
			RET
CKMAIORPOP:	POP HL
			POP DE
			OR 1
			RET
MEGMAI:		POP HL
			LD HL,(TPMEG)
			CALL DIV2
			LD (TPMAP),HL
			LD A,1
			LD (TMEMO),A
			POP HL
			RET
			
DIV2:		SRL H
			RR L
			RET
; --------------------- END | PROCEDURES TO DEFINE MEMORY TYPE TO BE USED				

; --------------------- BEGIN | PROCEDURE TO GET NUMBER OF MEMORY BLOCKS	
CKBLOC:		LD A,(BLOCOT)
			LD B,A
			CALL CTBLOC
			SUB B
			LD B,A
			LD HL,(TPAGES)
			LD A,H
			CP 1
			JR NZ,CBNO
			LD A,0FFH
			LD L,A
			JR CBPUL
CBNO:		LD A,L
CBPUL:		SUB B
			JR C,CBMEN
			LD A,1
			LD (FBLOC),A
			LD A,B
			RET
CBMEN:		LD A,(BLOCOT)
			LD B,A
			LD A,L
			ADD A,B
			LD (BLOCOT),A
			LD A,L
			RET
; --------------------- END | PROCEDURE TO GET NUMBER OF MEMORY BLOCKS				
	
; --------------------- BEGIN | PROCEDURE TO CHECK IF ESC KEY IS PRESSED		
CKESC:		DI
			IN A,(0AAH)
			AND 0F0H
			OR 7
			OUT(0AAH),A
			IN A,(0A9H)
			BIT 2,A
			JR Z,CELOOP
			RET
CELOOP:		IN A,(0AAH)
			AND 0F0H
			OR 7
			OUT(0AAH),A
			IN A,(0A9H)
			BIT 2,A
			JR Z,CELOOP
CELOOP2:	POP HL
			DJNZ CELOOP2	
			CALL SFPULA
			CALL RTELA
			JP COMECO
; --------------------- END | PROCEDURE TO CHECK IF ESC KEY IS PRESSED				
	
; --------------------- BEGIN | PROCEDURE TO MULTIPLY NUMBERS
MULTI:		LD A,B
			LD B,16
M16LOOP:	ADD HL,HL
			SLA C
			RLA
			JR NC,M16NO
			ADD HL,DE
M16NO:		DJNZ M16LOOP
			RET
; --------------------- END | PROCEDURE TO MULTIPLY NUMBERS		
	
; --------------------- BEGIN | PROCEDURE TO DIVIDE NUMBERS
DIV:		BIT 7,D
			JR Z,DIV15
			LD H,B
			LD L,C
			LD BC,1
			OR A
			SBC HL,DE
			RET NC
			ADD HL,DE
			DEC BC
			RET
DIV15:		LD A,B
			LD HL,0
			LD B,16
D15LOOP:	RL C
			RLA
			ADC HL,HL
			SBC HL,DE
D15NULL:	CCF
			JR NC,D15NEG
D15POS:		DJNZ D15LOOP
			RL C
			RLA
			LD B,A
			RET
D15REST:	RL C
			RLA
			ADC HL,HL
			AND A
			ADC HL,DE
			JR C,D15POS
			JR Z,D15NULL
D15NEG:		DJNZ D15REST
			RL C
			RLA
			ADD HL,DE
			LD B,A
			RET
; --------------------- END | PROCEDURE TO DIVIDE NUMBERS		

; --------------------- BEGIN | PROCEDURE TO GET NUMBER OF SECTORS AND BLOCKS
CTBLOC:		PUSH BC
			LD A,(TCOPIA)
			CP 0FCH
			JR Z,CTBFC
			CP 0FDH
			JR Z,CTBFD
			CP 0F8H
			JR Z,CTBF8
			LD BC,80
			LD A,2
			JR CTBPUL
CTBFC:		LD BC,40
			LD A,1
			JR CTBPUL
CTBFD:		LD BC,40
			LD A,2
			JR CTBPUL
CTBF8:		LD BC,80
			LD A,1
CTBPUL:		CP 1
			JR Z,CTBP1
			LD DE,18
			JR CTBCON
CTBP1:		LD DE,9
CTBCON:		LD A,(OVER)
			AND A
			JR Z,CTBC2
			INC BC
			INC BC
CTBC2:		CALL MULTI
			LD (TSET),HL
			PUSH HL
			PUSH HL
			POP BC
			LD DE,32
			CALL DIV
			LD (HLTEMP),BC
			LD DE,32
			CALL MULTI
			POP DE
			XOR A
			SBC HL,DE
			LD BC,(HLTEMP)
			JR NC,CTBI
			INC BC
CTBI:		LD A,C
			POP BC
			RET
; --------------------- END | PROCEDURE TO GET NUMBER OF SECTORS AND BLOCKS
	
; --------------------- BEGIN | PROCEDURE TO CALCULATE SECTOR NUMBER
CNSET:		LD HL,(TSET)
			LD A,(FLAGB)
			AND A
			JR Z,CNSD
			LD DE,(BLOCI)
			JR CNSETP
CNSD:		LD DE,(SETI)
CNSETP:		XOR A
			SBC HL,DE
			PUSH HL
			PUSH HL
			POP DE
			LD HL,32
			XOR A
			SBC HL,DE
			JR NC,CSETM
			POP DE
			LD DE,32
			JR CSETP
CSETM:		POP DE
CSETP:		LD A,E
			LD (NSET),A
			PUSH DE
			POP BC
			CALL MULTI4
			LD (QBLC),HL
			RET
; --------------------- END | PROCEDURE TO CALCULATE SECTOR NUMBER			

; --------------------- BEGIN | PROCEDURE TO SET UP DRIVES		
ACOPIA:		LD A,(OPERAC)
			AND A
			JR NZ,ACSAVE
			XOR A
			LD (OP),A
			LD A,(DRIVE1)
			LD (DRV),A
			LD A,(DRIVE2)
			INC A
			LD (NOME),A
			JR ACOPIAPUL
ACSAVE:		LD A,1
			LD (OP),A
			LD A,(DRIVE2)
			LD (DRV),A
			LD A,(DRIVE1)
			INC A
			LD (NOME),A
ACOPIAPUL:	LD A,(ACESSO)
			AND A
			RET Z
			LD A,(DRV)
			CALL CONVERTDRV
			LD (DRV),A
			RET
; --------------------- END | PROCEDURE TO SET UP DRIVES
	
; --------------------- BEGIN | PROCEDURE TO MULTIPLY BY 9
MULTI9:		PUSH BC
			POP HL
			ADD HL,HL
			ADD HL,HL
			ADD HL,HL
			ADD HL,BC
			RET
MULTI4:		PUSH BC
			POP HL
			ADD HL,HL
			ADD HL,HL
			RET
; --------------------- END | PROCEDURE TO MULTIPLY BY 9
	
; --------------------- BEGIN | PROCEDURE TO WRITE STRING IN VRAM AUTOMATICALLY
WVM:		DI
			LD A,E
			OUT (099H),A
			LD A,D
			OR 40H
			OUT (099H),A
			LD A,(HL)
			OUT (098H),A
			INC HL
			LD A,(HL)
			CP ','
			JR Z,WVMF
			INC DE
			JR WVM
WVMF:		RET
; --------------------- END | PROCEDURE TO WRITE STRING IN VRAM AUTOMATICALLY	
	
; --------------------- BEGIN | PROCEDURE TO WRITE BYTE IN VRAM	
WVBYTE:		DI
			PUSH HL
			PUSH DE
			PUSH BC
			PUSH AF
			LD A,L
			OUT (099H),A
			LD A,H
			AND 3FH
			OR 40H
			OUT (099H),A
			CALL DELAY
			POP AF
			OUT (098H),A
			POP BC
			POP DE
			POP HL
			RET
; --------------------- END | PROCEDURE TO WRITE BYTE IN VRAM		

; --------------------- BEGIN | PROCEDURES TO CONVERT LOGICAL TO PHYSICAL (LETTERS)
CONVERTDRV:	PUSH AF
			LD B,4
			LD C,0
			LD IX,0FB22H
			LD A,(INTERDISK)
			LD D,A
CONVERTLP:	LD A,(IX)
			AND A
			JR Z,CONVERTNOT
			CP D
			JR Z,CONVERTSIM
			LD A,(IX - 1)
			ADD A,C
			LD C,A
			INC IX
			INC IX
			DJNZ CONVERTLP
			JR CONVERTNOT
CONVERTSIM:	POP AF
			LD L,A
			LD A,(IX - 1)
			LD B,A
			LD E,0
			LD D,C
CONVERTLP2: LD A,D
			CP L
			JR Z,CONVERTFIM
			INC E
			INC D
			INC IY
			INC IY
			DJNZ CONVERTLP2
			XOR A
			RET
CONVERTFIM:	LD A,E
			RET
CONVERTNOT:	LD A,C
			AND A
			JR NZ,CONVERTSIM
			XOR A
			RET		

GETDRIVE:	LD IX,0FB22H
			LD IY,INTERFACES
			LD B,4
			LD D,A
			LD E,0
			LD C,0
			LD H,0
GETDRIVELP1:LD A,(IX - 1)
			AND A
			JR Z,GETDRIVENAO
			ADD A,C		
			LD C,A
			LD L,0
GETDRIVELP2:LD A,E
			CP D
			JR Z,GETDRIVESIM
			INC E
			INC L
			LD A,C
			CP E
			JR NZ,GETDRIVELP2
			DEC B
			JR Z,GETDRIVENAO
			INC IX
			INC IX
			INC IY
			JR GETDRIVELP1
GETDRIVENAO:XOR A
			RET
GETDRIVESIM:LD A,(IX)
			LD H,A
			LD A,(IX -1)
			LD C,A
			LD A,(IY)
			RET			
; --------------------- END | PROCEDURES TO CONVERT LOGICAL TO PHYSICAL (LETTERS)

; --------------------- BEGIN | PROCEDURE TO CALCULATE CHECKSUM
CHECKSUM: 	LD D,0
			LOOPCHK: LD E,(IX)
			ADD HL,DE
			INC IX
			DEC BC
			LD A,B
			OR C
			JR NZ, LOOPCHK
			RET
; --------------------- END | PROCEDURE TO CALCULATE CHECKSUM	
	
; --------------------- BEGIN | PROCEDURE TO PRINT 16BIT HEX NUMBERS	
PRINTHEX:	LD A,D
			AND 11110000B
			RRCA
			RRCA
			RRCA
			RRCA
			PUSH HL
			LD HL,HEXNUMBERS
			LD B,0
			LD C,A
			ADD HL,BC
			LD A,(HL)
			POP HL
			CALL WVBYTE
			INC HL
			LD A,D
			AND 0FH
			PUSH HL
			LD HL,HEXNUMBERS
			LD B,0
			LD C,A
			ADD HL,BC
			LD A,(HL)
			POP HL
			CALL WVBYTE
			INC HL
			LD A,E
			AND 11110000B
			RRCA
			RRCA
			RRCA
			RRCA
			PUSH HL
			LD HL,HEXNUMBERS
			LD B,0
			LD C,A
			ADD HL,BC
			LD A,(HL)
			POP HL
			CALL WVBYTE
			INC HL
			LD A,E
			AND 0FH
			PUSH HL
			LD HL,HEXNUMBERS
			LD B,0
			LD C,A
			ADD HL,BC
			LD A,(HL)
			POP HL
			CALL WVBYTE
			RET		
; --------------------- END | PROCEDURE TO PRINT 16BIT HEX NUMBERS	
			
; --------------------- BEGIN | PROCEDURE TO READ STRING FROM VRAM (SAME AS 0059H)
RVRAM:		DI
			LD A,L
			OUT (099H),A
			LD A,H
			AND 3FH
			OUT (099H),A
			CALL DELAY
RVRL:		IN A,(098H)
			LD (DE),A
			INC DE
			DEC BC
			LD A,C
			OR B
			JR NZ,RVRL
			RET
; --------------------- END | PROCEDURE TO READ STRING FROM VRAM (SAME AS 0059H)			
	
; --------------------- BEGIN | PROCEDURE TO WRITE STRING IN VRAM (SAME AS 005CH)	
WVRAM:		DI
			LD A,E
			OUT (099H),A
			LD A,D
			AND 3FH
			OR 40H
			OUT (099H),A
			CALL DELAY
WVRL:		LD A,(HL)
			OUT (098H),A
			DEC BC
			INC HL
			LD A,C
			OR B
			JR NZ,WVRL
			RET
; --------------------- END | PROCEDURE TO WRITE STRING IN VRAM (SAME AS 005CH)			
	
; --------------------- BEGIN | PROCEDURE TO INITIALIZE SLOT
INITSLOT:	DI
			LD A,(0FCC1H)
			LD D,A
			IN A,(0A8H)
			LD C,A
			AND 00110000B
			OUT(0A8H),A
			LD A,(0FFFFH)
			CPL
			AND 11111100B
			LD B,A
			LD A,D
			AND 00001100B
			RRCA
			RRCA
			OR B
			LD (0FFFFH),A
			LD A,C
			AND 11110000B
			OUT(0A8H),A
			RET
; --------------------- BEGIN | PROCEDURE TO INITIALIZE SLOT			
	
; --------------------- BEGIN | PROCEDURE TO EXIT TO DOS
FIMDOS:		DI
			LD A,(0F341H)
			LD (PAGE0RAM),A
			LD A,(0F342H)
			LD (PAGE1RAM),A
			CALL FIMMAP
			LD A,(PAGE1RAM)
			AND 3
			RLCA
			RLCA
			LD B,A
			LD A,(PAGE0RAM)
			AND 3
			OR B
			LD B,A
			IN A,(0A8H)
			AND 11110000B
			OR B
			OUT(0A8H),A
			PUSH AF
			LD A,(PAGE1RAM)
			PUSH AF
			AND 3
			RRCA
			RRCA
			LD B,A
			IN A,(0A8H)
			AND 00111111B
			OR B
			OUT(0A8H),A
			POP AF
			AND 00001100B
			LD B,A
			LD A,(0FFFFH)
			CPL
			AND 11110011B
			OR B
			LD (0FFFFH),A			
			LD A,(PAGE0RAM)
			PUSH AF
			AND 3
			RRCA
			RRCA
			LD B,A
			IN A,(0A8H)
			AND 00111111B
			OR B
			OUT(0A8H),A
			POP AF
			AND 00001100B
			RRCA
			RRCA
			LD B,A
			LD A,(0FFFFH)
			CPL
			AND 11111100B
			OR B
			LD (0FFFFH),A	
			POP AF
			OUT(0A8H),A
			LD A,0C3H
			LD (0000H),A
			EI
			RET
; --------------------- END | PROCEDURE TO EXIT TO DOS	

; --------------------- BEGIN | PROCEDURE TO MULTIPLY BY 8	
MULT8:		LD D,0
			LD L,D
			LD B,8
MULT8_LOOP:	ADD HL,HL
			JR NC,MULT8_NOADD
			ADD HL,DE
MULT8_NOADD:DJNZ MULT8_LOOP
			RET		
; --------------------- END | PROCEDURE TO MULTIPLY BY 8				

; --------------------- BEGIN | PROCEDURE TO DIVIDE BY 9	
DIV9:		PUSH BC
			POP HL
			INC HL
			LD D,H
			LD E,L  
			ADD HL,HL
			ADD HL,HL
			ADD HL,HL
			SBC HL,DE
			LD E,0
			LD D,L
			LD A,H
			ADD HL,HL
			ADD HL,HL
			ADD HL,DE
			ADC A,E
			XOR H
			AND 03FH
			XOR H
			RLCA
			RLCA
			LD BC,0
			LD C,A
			RET 
; --------------------- END | PROCEDURE TO DIVIDE BY 9		

; --------------------- BEGIN | PROCEDURE TO MULTIPLY 16 BIT NUMBER BY 8 BIT NUMBER (A*DE=HL)
MULT12:		LD L,0
			LD B,8
MULT12_LOOP:ADD HL,HL
			ADD A,A
			JR NC,MULT12_NADD
			ADD HL,DE
MULT12_NADD:DJNZ MULT12_LOOP
			RET	
; --------------------- END | PROCEDURE TO MULTIPLY 16 BIT NUMBER BY 8 BIT NUMBER (A*DE=HL)		

; --------------------- BEGIN | PROCEDURE TO DIVIDE 16 BIT NUMBERS (BC/DE=BC HL=REST)
DIV16:		LD HL,0
			LD A,B
			LD B,8
DIV16_LOOP1:RLA
			ADC HL,HL
			SBC HL,DE
			JR NC,DIV16_NADD1
			ADD HL,DE
DIV16_NADD1:DJNZ DIV16_LOOP1
			LD B,A
			LD A,C
			LD C,B
			LD B,8
DIV16_LOOP2:RLA
			ADC HL,HL
			SBC HL,DE
			JR NC,DIV16_NADD2
			ADD HL,DE
DIV16_NADD2:DJNZ DIV16_LOOP2
			RLA
			CPL
			LD B,A
			LD A,C
			LD C,B
			RLA
			CPL
			LD B,A
			RET	
; --------------------- END | PROCEDURE TO DIVIDE 16 BIT NUMBERS (BC/DE=BC HL=REST)

; --------------------- BEGIN | PROCEDURE TO DRAW A BAR
WVBINV:		PUSH HL
			PUSH DE
			PUSH DE
			LD A,(HL)
			CP '.'
			JR Z,WVBIP
			ADD A,20H
			JR WVBIC
WVBIP:		SUB 20H
WVBIC:		POP HL
			PUSH BC
			CALL 4DH
			POP BC
			POP DE
			POP HL
			DEC BC
			INC HL
			INC DE
			LD A,C
			OR B
			JR NZ,WVBINV
			RET   
; --------------------- END | PROCEDURE TO DRAW A BAR			

; --------------------- BEGIN | PROCEDURE TO READ A CHARACTER FROM KEYBOARD
; --------------------- BEGIN | MAIN ROUTINE
CHGET:		CALL 9CH
			JR Z,CHGET
			CALL 9FH
			JP ABYTE
; --------------------- END | MAIN ROUTINE
; --------------------- BEGIN | AUXILIARY ROUTINE			
ABYTE:		PUSH AF
			SUB 61H
			JR C,ABSAI
			POP AF
			PUSH AF
			SUB 7BH
			JR NC,ABSAI
			POP AF
			SUB 20H
			RET
ABSAI:		POP AF
			RET		
; --------------------- END | AUXILIARY ROUTINE			
; --------------------- END | PROCEDURE TO READ A CHARACTER FROM KEYBOARD			
	
; --------------------- BEGIN | PROCEDURE TO DRAW CURSOR
CHGCUR:		PUSH HL
			CALL 4AH
			LD HL,0
			LD L,A
			ADD HL,HL
			ADD HL,HL
			ADD HL,HL
			LD DE,800H
			ADD HL,DE
			LD DE,BUFGUI
			LD BC,8
			CALL 59H
			LD HL,BUFGUI
			LD B,8
CHGLP:		LD A,(HL)
			CPL
			LD (HL),A
			INC HL
			DJNZ CHGLP
			LD HL,BUFGUI
			LD DE,800H
			LD BC,8
			CALL 5CH
			POP HL
			XOR A
			JP 4DH
; --------------------- END | PROCEDURE TO DRAW CURSOR			
	
; --------------------- BEGIN | PROCEDURE TO DRAW A BAR IN CORRECT POSITION	
PBARRA:		LD A,(ATUAL)
			CP 1
			JP Z,BARRA1
			CP 2
			JP Z,BARRA2
			CP 3
			JP Z,BARRA3
			CP 4
			JP Z,BARRA4
			CP 5
			JP Z,BARRA5
			CP 6
			JP Z,BARRA6
; --------------------- END | PROCEDURE TO DRAW A BAR IN CORRECT POSITION				
	
; --------------------- BEGIN | PROCEDURE CREATE A MOVING WINDOW
JANELA:		LD (SPEED),IX
			JAN1:CALL WINDOW
			CALL NDELAY
			DEC C
			JR Z,JAN2
			DEC L
			INC E
			JR JAN1
			JAN2:DEC B
			RET Z
			DEC H
			INC D
			CALL WINDOW
			CALL NDELAY
			JR JAN2
; --------------------- END | PROCEDURE CREATE A MOVING WINDOW
	
; --------------------- BEGIN | PROCEDURES TO CREATE A WINDOW
; --------------------- BEGIN | MAIN WINDOW PROCEDURE	
WINDOW:		PUSH HL
			PUSH DE
			PUSH BC
			JR WINCONT
EVRAM:DW 0
PONTOS:DW 0
FLAGP:DW 0
XYORI:DW 0
XYDES:DW 0
SPEED:DW 0
WINCONT:	LD (XYORI),HL
			LD (XYDES),DE
			CALL WINCKFI
			JP C,WDFIM
			LD HL,(XYORI)
			LD DE,(XYDES)
			LD E,D
			LD D,H
			PUSH DE
			CALL VRAMPOS
			LD BC,0017H
			LD IX,1819H
			CALL LINHA
			LD HL,(XYORI)
			LD DE,(XYDES)
			LD L,E
			CALL VRAMPOS
			POP DE
			LD BC,0017H
			LD IX,1A1BH
			CALL LINHA
			LD HL,(XYORI)
			LD DE,(XYDES)
			LD D,L
			PUSH DE
			CALL VRAMPOS
			LD BC,0116H
			LD IX,181AH
			CALL LINHA
			LD HL,(XYORI)
			LD DE,(XYDES)
			LD H,D
			CALL VRAMPOS
			POP DE
			LD BC,0116H
			LD IX,191BH
			CALL LINHA
			JR WDFIM
; --------------------- END | MAIN WINDOW PROCEDURE		
		
; --------------------- BEGIN | AUXILIARY WINDOW PROCEDURES		
LINHA:		LD (FLAGP),BC
			LD (PONTOS),DE
			PUSH IX
			POP DE
			LD A,D
			CALL WVBYTE
LOOPL:		CALL INCVRAM
			CALL CHKLIN
			JR C,LINHAF
			LD A,(FLAGP)
			CALL WVBYTE
			JR LOOPL
LINHAF:		PUSH IX
			POP DE
			LD A,E
			JP WVBYTE
	
INCVRAM:	LD BC,(FLAGP)
			LD A,B
			AND A
			JR Z,INCV1
			LD DE,40
			ADD HL,DE
			RET
INCV1:		INC HL
			RET
	
CHKLIN:		LD DE,(PONTOS)
			LD A,E
			DEC A
			SUB D
			JR Z,CHKLF
			XOR A
			JR CKLIP
CHKLF:		SCF
CKLIP:		INC D
			LD (PONTOS),DE
			RET
WDFIM:		CALL WINFIL
			POP BC
			POP DE
			POP HL
			RET
	
VRAMPOS:	PUSH DE
			LD C,L
			LD B,H
			LD HL,0
			LD DE,40
VRLOOP:		LD A,C
			AND A
			JR Z,VRPPUL
			DEC C
			ADD HL,DE
			JR VRLOOP
VRPPUL:		LD DE,0
			LD E,B
			ADD HL,DE
			POP DE
			RET
WINCKFI:	LD A,D
			DEC A
			SUB H
			JR C,WINBAD
			LD A,E
			DEC A
			SUB L
			JR C,WINBAD
			XOR A
			RET
WINBAD:		SCF
			RET
	
WINFIL:		LD HL,(XYORI)
			LD DE,(XYDES)
			LD A,D
			DEC A
			SUB H
			RET Z
			LD A,E
			DEC A
			SUB L
			RET Z
			INC H
			INC L
			CALL VRAMPOS
			PUSH HL
			LD HL,(XYORI)
			LD A,E
			DEC A
			SUB L
			LD C,A
			LD A,D
			DEC A
			SUB H
			LD B,A
			POP HL
			LD (EVRAM),HL
WFLP:		PUSH BC
WFLOOP:		LD A,32
			CALL WVBYTE
			INC HL
			DEC B
			JR NZ,WFLOOP
			LD HL,(EVRAM)
			LD DE,40
			ADD HL,DE
			LD (EVRAM),HL
			POP BC
			DEC C
			JR NZ,WFLP
			RET
; --------------------- END | AUXILIARY WINDOW PROCEDURES					
; --------------------- END | PROCEDURES TO CREATE A WINDOW			
	
; --------------------- BEGIN | PROCEDURE TO DELAY THE WINDOW EFFECT	
NDELAY:		PUSH BC
			LD BC,(SPEED)
			NDY:LD A,C
			OR B
			JR Z,NDELSAI
			DEC BC
			JR NDY
			NDELSAI:POP BC
			RET
; --------------------- END | PROCEDURE TO DELAY THE WINDOW EFFECT			

; --------------------- BEGIN | PROCEDURE TO PRINT A STRING		
PRINTH:		LD (PHELPH),HL
PHLOOP:		LD A,(DE)
			CP 13
			JR Z,PRINTHZ
			AND A
			RET Z
			CALL WVBYTE
			INC HL
			INC DE
			JR PHLOOP
PRINTHZ:	INC DE
			PUSH DE
			LD HL,(PHELPH)
			LD DE,40
			ADD HL,DE
			LD (PHELPH),HL
			POP DE
			JR PHLOOP
; --------------------- END | PROCEDURE TO PRINT A STRING			

; --------------------- BEGIN | PROCEDURE TO DISPLAY INFORMATION ABOUT MEMORY FOUND	
; --------------------- BEGIN | MAIN ROUTINE
MEMF:		LD A,(TMEMO)
			CP 2
			RET Z
			PUSH AF
			CALL STELAP
			LD HL,130FH
			LD DE,1410H
			LD BC,1204H
			LD IX,400H
			CALL JANELA
			LD DE,DISPMEM
			LD HL,529
			CALL PRINTH
			LD DE,ANYKEY
			LD HL,727
			CALL PRINTH  
			CALL CHKMEM
			POP AF
			AND A
			JR Z,MFMAP
			PUSH DE
			LD IX,8
			CALL MEMFM
			POP DE
			CALL PTSLSI
			LD HL,DISPMEG
			LD DE,576
			LD BC,13
			CALL WVRAM
MEMFL:		CALL CHGET
			CP 27
			JP NZ,RTELA
			POP HL
			POP HL
			CALL RTELA
			JP COMECO
MFMAP:		PUSH HL
			PUSH IX
			POP IY
			LD IX,16
			CALL MEMFM
			POP DE
			CALL PTSLSI
			LD HL,DISPMAP
			LD DE,576
			LD BC,13
			CALL WVRAM
			JR MEMFL
; --------------------- END | MAIN ROUTINE				
; --------------------- BEGIN | AUXILIARY ROUTINES			
PTSLSI:		LD A,D
			ADD A,30H
			LD HL,616
			CALL WVBYTE
			LD A,E
			ADD A,30H
			LD HL,618
			CALL WVBYTE
			PUSH IY
			POP HL
			LD DE,656
			JP PRINT
			
MEMFM:		PUSH IY
			POP HL
			LD A,(TMEMO)
			AND A
			JR NZ,MEMFP
			LD DE,4
			ADD HL,DE
MEMFP:		PUSH HL
			POP BC
			PUSH IX
			POP DE
			CALL MULTI
			PUSH HL
			POP IY
			RET
; --------------------- END | AUXILIARY ROUTINES				
; --------------------- END | PROCEDURE TO DISPLAY INFORMATION ABOUT MEMORY FOUND			

; --------------------- BEGIN | PROCEDURE TO CHECK IF THERE IS SPECIAL MEMORY
MEMFCK:		LD HL,(TPAGES)
			LD DE,(TPAGESM)
			LD A,L
			CP E
			JR NZ,MEMFCKD
			LD A,H
			CP D
			JR NZ,MEMFCKD
			LD A,(TMEMO)
			LD B,A
			LD A,(TMEMOM)
			CP B
			JR NZ,MEMFCKD
			XOR A
			RET
MEMFCKD:	LD HL,(TPAGES)
			LD (TPAGESM),HL
			LD A,(TMEMO)
			LD (TMEMOM),A
			SCF
			RET 
; --------------------- END | PROCEDURE TO CHECK IF THERE IS SPECIAL MEMORY			
	
; --------------------- BEGIN | PROCEDURE TO CALCULATE THE NUMBER OF BLOCKS OF 128 BYTES
GETQTDBLOCKS:
			LD A,(TCOPIA)
			CP 0FCH
			JR Z,T180
			CP 0FDH
			JR Z,T360
			CP 0F8H	
			JR Z,T360
			LD BC,5760
			JR CALCCONT
T180:		LD BC,1440
			JR CALCCONT
T360:		LD BC,2880
CALCCONT:	LD (QTDBLOCKS),BC
			RET
; --------------------- END | PROCEDURE TO CALCULATE THE NUMBER OF BLOCKS OF 128 BYTES

; --------------------- BEGIN | PROCEDURE TO SET UP FDC
CHAVEIA:	DI
			LD (ADDRESS),HL
			PUSH AF
			PUSH HL
			PUSH DE
			PUSH BC
			LD (CMDS),BC
; --------------------- BEGIN | ROUTINE TO SET PAGE 1 TO FDC
			LD A,B
			CP 8
			JR NZ,DATAJUMP
			LD HL,(ADDRESS)
			LD DE,BUFFERADDR
			LD BC,512
			LDIR
DATAJUMP:	LD HL,(INTERDISCO)
			IN A,(0A8H)
			LD D,A
			LD (SLOT1),A
			LD A,H
			RLCA
			RLCA
			RLCA
			RLCA
			OR H
			RLCA
			RLCA
			LD B,A
			LD A,D
			AND 00110011B
			OR B
			OUT(0A8H),A
			LD (SLOT2),A
			LD C,A
			LD A,(0FFFFH)
			CPL
			LD (SUBSLOT),A
			AND 11110011B
			LD B,A
			LD A,L
			AND 01111111B
			RLCA
			RLCA
			OR B
			LD (0FFFFH),A
			LD A,C
			AND 00111111B
			LD B,A
			LD A,(SLOT1)
			AND 11000000B
			OR B
			OUT(0A8H),A
; --------------------- END | ROUTINE TO SET PAGE 1 TO FDC
			LD BC,(CMDS)
			LD A,B
			AND A
			JR Z,SETINTERFACE
			CP 1
			JP Z,RESTOREDRV
			CP 2
			JP Z,SEEKTRACK
			CP 3
			JP Z,DESLIDRIVE
			CP 4
			JP Z,SETACTUALTRACK
			CP 5
			JP Z,SETNEXTTRACK
			CP 6
			JP Z,SETSECTOR
			CP 7
			JP Z,READSECTOR
			CP 8
			JP Z,WRITESECTOR
			CP 9
			JP Z,GETATUALTRACK	
			CP 10
			JP Z,CHECKDOS2
			CP 11
			JP Z,INTERRUPT
			CP 12
			JP Z,CHECKPROT
; --------------------- BEGIN | ROUTINE TO IDENTIFY THE FDC CHIPSET		
SETINTERFACE:
			CALL GETINTERFACE
			JP SAIDA
; --------------------- END | ROUTINE TO IDENTIFY THE FDC CHIPSET	
; --------------------- BEGIN | ROUTINES TO RESTORE DRIVE						
RESTOREDRV:	CALL LIGDRIVE
			LD A,(INTERFACE)
			CP 3
			JR Z,DESLIDRIVE
			CP 4
			JR Z,RESTORECDX2
			LD A,1
			LD HL,(REG_CMD)
			LD (HL),A
			CALL DELAY2
			CALL WAIT2RES
			JP DESLIDRIVE
RESTORECDX2:LD A,1
			OUT(0D0H),A
			CALL DELAY2
			CALL WAITRES
			JP DESLIDRIVE
RESTORETOSHIBA:
			LD HL,1388H
LOOPDEV2:	DEC HL
			LD A,L
			OR H
			JR Z,CONT2
			LD DE,CMD_SENSE_DEV
			CALL WAIT_10_CALL
			CALL GUARDA_CMD
			LD A,(BUFFERIX)
			BIT 5,A
			JR Z,LOOPDEV2	
CONT2:		LD BC,(DRIVELADO)
			DEC B
			LD A,B
			LD (CMD_RECALIBRATE + 2),A	
			LD DE,CMD_RECALIBRATE
			CALL WAIT_10_CALL
			;CALL WAIT_10_FULL
			LD DE,CMD_SENSE_INT
			CALL WAIT_10_CALL
			JP DESLIDRIVE
; --------------------- END | ROUTINES TO RESTORE DRIVE		
; --------------------- BEGIN | ROUTINES TO MOVE THE DRIVES HEAD TO NEXT TRACK			
SEEKTRACK:	CALL LIGDRIVE
			LD A,(INTERFACE)
			CP 3
			JP Z,SAIDA
			CP 4
			JR Z,SEEKCDX2			
			LD A,10H
			LD HL,(REG_CMD)
			LD (HL),A
			CALL DELAY2
			CALL WAIT2
			CALL INTNOW
			JP SAIDA
SEEKCDX2:	LD A,11H
			OUT(0D0H),A
			CALL DELAY2
			CALL WAIT
			JP SAIDA
; --------------------- END | ROUTINES TO MOVE THE DRIVES HEAD TO NEXT TRACK
; --------------------- BEGIN | ROUTINES TO TURN OF DRIVE
DESLIDRIVE:	CALL DESLIGDRIVE
			JP SAIDA
			
DESLIGDRIVE:LD A,0
			LD (DRIVESTATUS),A
			LD A,(INTERFACE)
			CP 3
			JP Z,DESDRVTOSHIBA
			CP 4
			JR Z,DESDRVCDX2
			LD A,3
			LD HL,(REG_DRIVE)
			LD (HL),A
			RET
DESDRVCDX2:	XOR A
			OUT(0D4H),A
			RET			
DESDRVTOSHIBA:
			A,4
			LD IY,(REG_CONTROL0)
			LD (IY),A		
			RET
; --------------------- END | ROUTINES TO TURN OF DRIVE		
; --------------------- BEGIN | ROUTINES TO SET UP ACTUAL TRACK	
SETACTUALTRACK:
			LD A,(INTERFACE)
			CP 3
			JP Z,SAIDA
			CP 4
			JR Z,SETACTUALCDX2
			LD A,(ACESSO)
			AND A
			JP Z,SAIDA
			LD A,C
			LD HL,(REG_TRACK)
			LD (HL),A
			JP SAIDA
SETACTUALCDX2:
			LD A,(ACESSO)
			AND A
			JP Z,SAIDA
			LD A,C
			OUT(0D1H),A
			JP SAIDA
; --------------------- END | ROUTINES TO SET UP ACTUAL TRACK	
; --------------------- BEGIN | ROUTINES TO SET UP NEXT TRACK			
SETNEXTTRACK:
			LD A,(INTERFACE)
			CP 3
			JP Z,SAIDA
			CP 4
			JR Z,SETNEXTCDX2
			LD A,C
			LD HL,(REG_DATA)
			LD (HL),A
			JP SAIDA
SETNEXTCDX2:LD A,C
			OUT(0D3H),A
			JP SAIDA
; --------------------- END | ROUTINES TO SET UP NEXT TRACK		
; --------------------- BEGIN | ROUTINES TO SET UP SECTOR NUMBER	
SETSECTOR:	LD A,(INTERFACE)
			CP 3
			JP Z,SAIDA
			CP 4
			JR Z,SETSECTCDX2
			LD A,C
			LD HL,(REG_SECTOR)
			LD (HL),A
			JP SAIDA
SETSECTCDX2:LD A,C
			OUT(0D2H),A
			JP SAIDA
; --------------------- END | ROUTINES TO SET UP SECTOR NUMBER	
; --------------------- BEGIN | ROUTINES TO READ SECTOR		
READSECTOR:	CALL LIGDRIVE
			LD HL,BUFFERADDR
			LD A,(INTERFACE)
			CP 1
			JR Z,READ4SET
			CP 3
			JP Z,READ3
			CP 4
			JR Z,READCDX2			
			JR READ2SET
READBAK:	BIT 2,A
			JR NZ,READSECTOR
			LD DE,BUFFERADDR
			XOR A
			SBC HL,DE
			LD A,L
			AND A
			JP NZ,READSECTOR
			LD A,H
			CP 2
			JP NZ,READSECTOR
			JP SAIDA
READ2SET:	CALL READ2
			JP READBAK			
READ4SET:	CALL READ4
			JP READBAK
READCDX2:	CALL READ
			JR READBAK
; --------------------- END | ROUTINES TO READ SECTOR		
; --------------------- BEGIN | ROUTINES TO WRITE SECTOR			
WRITESECTOR:CALL LIGDRIVE
			LD HL,BUFFERADDR
			LD A,(INTERFACE)
			CP 3
			JP Z,READ3
			CP 4
			JR Z,WRITECDX2			
			LD A,(INTERFACE)
			CP 1
			JR Z,WRITE4SET			
			JR WRITE2SET
WRITEBAK:	BIT 6,A
			JR NZ,WRITEPROT
			BIT 2,A
			JR NZ,WRITESECTOR
			LD DE,BUFFERADDR
			XOR A
			SBC HL,DE
			LD A,L
			AND A
			JP NZ,WRITESECTOR
			LD A,H
			CP 2
			JP NZ,WRITESECTOR
			JP SAIDA
WRITE2SET:  CALL WRITE2
			JP WRITEBAK				
WRITE4SET:	CALL WRITE4
			JP WRITEBAK			
WRITECDX2:	CALL WRITE
			JR WRITEBAK

WRITEPROT:	CALL DESLIGDRIVE
			CALL WINDOWPROT
			JR WRITESECTOR
; --------------------- END | ROUTINES TO WRITE SECTOR
; --------------------- BEGIN | ROUTINES TO GET ACTUAL TRACK			
GETATUALTRACK:
			LD A,(INTERFACE)
			CP 3
			JP Z,SAIDA
			CP 4
			JR Z,GETATUALCDX2
			LD HL,(REG_TRACK)
			LD A,(HL)
			LD (TATUALBUF),A
			JP SAIDA
GETATUALCDX2:
			IN A,(0D1H)
			LD (TATUALBUF),A
			JP SAIDA		
; --------------------- END | ROUTINES TO GET ACTUAL TRACK
; --------------------- BEGIN | ROUTINE TO VERIFY THE PRESENCE OF DOS2
CHECKDOS2:	
			LD IY,STR_DOS2
			CALL SEARCH_BYTES
			LD (DOS2),A
			JP SAIDA
; --------------------- EBD | ROUTINE TO VERIFY THE PRESENCE OF DOS2
; --------------------- BEGIN | ROUTINE TO INTERRUPT COMMANDS
INTERRUPT:	EX (SP),HL
			EX (SP),HL
			EX (SP),HL
			EX (SP),HL
			LD A,0D0H
			LD DE,(REG_CMD)
			LD (DE),A
			JP SAIDA
; --------------------- END | ROUTINE TO INTERRUPT COMMANDS
; --------------------- BEGIN | ROUTINE TO CHECK IF DISK IS WRITE PROTECTED
CHECKPROT:	CALL LIGDRIVE
			LD HL,BUFFERADDR
			LD A,(INTERFACE)
			CP 4
			JR Z,CHECKCDX2	
			CALL WRITE3
CHECKBACK:	BIT 6,A
			JP Z,CHECKSAIDA	
			CALL DESLIGDRIVE
			CALL WINDOWPROT
			JR CHECKPROT
CHECKCDX2:	LD A,0A0H
			OUT(0D0H),A
			CALL DELAY2
			LD B,100
CHECKCDX2B:	IN A,(0D0H)
			ADD A,A
			JP M,CKCDX2PUL
			DJNZ CHECKCDX2B
CKCDX2PUL:	LD A,0D0H
			OUT(0D0H),A
			CALL DELAY2
			CALL WAIT
			IN A,(0D0H)
			JR CHECKBACK	
CHECKSAIDA:	CALL DESLIGDRIVE
			JP SAIDA
; --------------------- END | ROUTINE TO CHECK IF DISK IS WRITE PROTECTED

; --------------------- BEGIN | ROUTINE TO RESTORE PAGE 1 ORIGINAL CONFIGURATION
SAIDA:		LD A,(SLOT2)
			OUT(0A8H),A
			LD A,(SUBSLOT)
			LD (0FFFFH),A
			LD A,(SLOT1)
			OUT(0A8H),A
			LD BC,(CMDS)
			LD A,B
			CP 7
			JR NZ,DATAJUMP2
			LD HL,BUFFERADDR
			LD DE,(ADDRESS)
			LD BC,512
			LDIR
			LD IX,BUFFERADDR
			LD HL,(CHECKCALC)
			LD BC,512
			CALL CHECKSUM
			LD (CHECKCALC),HL		
DATAJUMP2:	POP BC
			POP DE
			POP HL
			POP AF
			RET	
; --------------------- END | ROUTINE TO RESTORE PAGE 1 ORIGINAL CONFIGURATION
; --------------------- END | PROCEDURE TO SET UP FDC

; --------------------- BEGIN | ROUTINES TO INTERRUPT COMMANDS
INTWAIT:	EX (SP),HL
			EX (SP),HL
			EX (SP),HL
			EX (SP),HL
			RET
			
INTNOW:		CALL INTWAIT	
			LD A,0D0H
			LD DE,(REG_CMD)
			LD (DE),A
			CALL INTWAIT
INTNOWBACK:	LD A,(DE)
			RRA
			RET NC			
			JR INTNOWBACK
			
INTNOWCDX2:	CALL INTWAIT	
			LD A,0D0H
			OUT(0D0H),A
			CALL INTWAIT
INTNOWBACK2:IN A,(0D0H)
			RRA
			RET NC			
			JR INTNOWBACK2			
; --------------------- END | ROUTINES TO INTERRUPT COMMANDS

; --------------------- BEGIN | PROCEDURES TO ACCESS W.DIGITAL AND FUJITSU FDC	
; ---------------------	
; --------------------- BEGIN | PROCEDURES TO WAIT DEVICE IS READY USING  W.DIGITAL AND FUJITSU FDC
WAIT2:		LD HL,(REG_CMD)
			LD A,(HL)
			RRA
			RET NC
			JR WAIT2

WAIT2RES:	LD HL,2
WAIT2RESBA:	LD BC,0FFFFH
WAIT2RESBAB:LD A,C
			OR B
			JR Z,WAIT2SAI
			DEC BC
			LD DE,(REG_CMD)
			LD A,(DE)
			RRA
			RET NC
			JR WAIT2RESBAB	
WAIT2SAI:	DEC HL
			LD A,L
			OR H
			RET Z
			JR WAIT2RESBA						
; --------------------- END | PROCEDURES TO WAIT DEVICE IS READY USING  W.DIGITAL AND FUJITSU FDC		
		
; --------------------- BEGIN | PROCEDURES TO TURN ON DRIVE USING W.DIGITAL AND FUJITSU FDC
LIGDRIVE:	LD HL,(DRIVELADO)
			LD A,(DRIVESTATUS)
			AND A
			JR Z,LIGCONT
			LD A,(ACTUALSIDE)
			CP L
			RET Z
LIGCONT:	LD A,1
			LD (DRIVESTATUS),A
			LD A,L
			LD (ACTUALSIDE),A			
			LD A,(INTERFACE)
			CP 2
			JR Z,NATION
			CP 3
			JR Z,LIGTOSHIBA
			CP 4
			JR Z,LIGMICROSOL
			LD A,L
			LD DE,(REG_SIDE)
			LD (DE),A
			LD A,H
			SUB 1
			LD B,A
			LD A,0C4H
			OR B
			LD DE,(REG_DRIVE)
			LD (DE),A
			CALL DELAY2
			CALL LIGWAIT			
			RET
NATION:		LD DE,(REG_DRIVE)
			LD A,01001000B
			LD B,A
			LD A,H
			OR B
			LD B,A
			LD A,L
			RLCA
			RLCA
			OR B
			LD (DE),A
			CALL DELAY2
			CALL LIGWAIT			
			RET
LIGMICROSOL:RLC L
			RLC L
			RLC L
			RLC L
			LD A,20H
			OR L
			OR H
			OUT(0D4H),A
			CALL DELAY2
			RET		
LIGTOSHIBA:	DEC H
			LD A,H
			INC A
			RRCA
			RRCA
			RRCA
			RRCA
			AND 11110000B
			OR 00000100B
			OR H
			LD IY,(REG_CONTROL0)
			LD (IY),A
			RET			
			
LIGWAIT:	LD BC,0
			LD DE,(REG_CMD)
LIGBACK:	LD A,(DE)
			AND 80H
			RET Z
			DEC BC
			LD A,B
			OR C
			JP NZ,LIGBACK	
			RET
; --------------------- END | PROCEDURES TO TURN ON DRIVE USING W.DIGITAL AND FUJITSU FDC
	
; --------------------- BEGIN | PROCEDURES TO READ DATA USING W.DIGITAL AND FUJITSU FDC 			
READ2:		LD DE,READ2SAI
			PUSH DE
			LD DE,(REG_CMD)
			LD A,80H
			LD (DE),A
			LD DE,0
			LD BC,(REG_STATUS_2)	
READ2BACK1:	DEC DE
			LD A,E
			OR D
			RET Z
			LD A,(BC)
			ADD A,A
			JP C,READ2BACK1
			LD DE,(REG_DATA)
READ2BACK2:	LD A,(BC)
			ADD A,A
			RET C
			JP M,READ2BACK2
			LD A,(DE)
			LD (HL),A
			INC HL
			JP READ2BACK2	
READ2SAI:	LD DE,(REG_CMD)
			LD A,(DE)
			PUSH AF
			CALL INTNOW
			POP AF
			RET			
		
READ4:		LD DE,READ2SAI
			PUSH DE
			LD DE,(REG_CMD)
			LD A,80H
			LD (DE),A
			LD DE,0
			LD BC,(REG_STATUS_2)
READ4BACK1:	DEC DE
			LD A,E
			OR D
			RET Z
			LD A,(BC)
			ADD A,A
			JP P,READ4BACK1
			LD DE,(REG_DATA)
READ4BACK2:	LD A,(BC)
			ADD A,A
			RET P
			JP C,READ4BACK2
			LD A,(DE)
			LD (HL),A
			INC HL
			JP READ4BACK2			
; --------------------- END | PROCEDURES TO READ DATA USING W.DIGITAL AND FUJITSU FDC 			

; --------------------- BEGIN | PROCEDURES TO WRITE DATA USING W.DIGITAL AND FUJITSU FDC 		
WRITE2:		LD DE,READ2SAI
			PUSH DE
			LD A,0A0H
			LD DE,(REG_CMD)
			LD (DE),A
			CALL DELAY2		
			LD A,(DE)
			ADD A,A
			RET M
			LD DE,0
			LD BC,(REG_STATUS_2)	
WRITE2BACK1:DEC DE
			LD A,E
			OR D
			RET Z
			LD A,(BC)
			ADD A,A
			JP C,WRITE2BACK1
			LD DE,(REG_DATA)
WRITE2BACK2:LD A,(BC)
			ADD A,A
			RET C
			JP M,WRITE2BACK2
			LD A,(HL)
			LD (DE),A
			INC HL
			JP WRITE2BACK2			
			
WRITE4:		LD DE,READ2SAI
			PUSH DE
			LD A,0A0H
			LD DE,(REG_CMD)
			LD (DE),A
			CALL DELAY2
			LD A,(DE)
			ADD A,A
			RET M	
			LD DE,0
			LD BC,(REG_STATUS_2)
WRITE4BACK1:DEC DE
			LD A,E
			OR D
			RET Z
			LD A,(BC)
			ADD A,A
			JP P,WRITE4BACK1
			LD DE,(REG_DATA)			
WRITE4BACK2:LD A,(BC)
			ADD A,A
			RET P
			JP C,WRITE4BACK2
			LD A,(HL)
			LD (DE),A
			INC HL
			JP WRITE4BACK2	

WRITE3:		LD DE,READ2SAI
			PUSH DE
			LD B,100
			LD A,0A0H
			LD DE,(REG_CMD)
			LD (DE),A
			CALL DELAY2		
WRITE3BACK:	LD A,(DE)
			ADD A,A
			RET M
			DJNZ WRITE3BACK
			RET		
; --------------------- END | PROCEDURES TO WRITE DATA USING W.DIGITAL AND FUJITSU FDC			
; ---------------------			
; --------------------- END | PROCEDURES TO ACCESS W.DIGITAL AND FUJITSU FDC			
			
; --------------------- BEGIN | PROCEDURES TO ACCESS TOSHIBA FDC
; ---------------------
; --------------------- BEGIN | PROCEDURE TO SAVE THE DEVICE STATUS	IN MEMORY
GUARDA_CMD:	LD IX,BUFFERIX
LOOPADD:	LD IY,(REG_STATUS)
			LD A,(IY)
			ADD A,A
			JR NC,LOOPADD
			RET P
			LD IY,(REG_RESULT_STATUS)
			LD A,(IY)
			LD (IX),A
			INC IX
			JR LOOPADD
; --------------------- END | PROCEDURE TO SAVE THE DEVICE STATUS	IN MEMORY			

; --------------------- BEGIN | PROCEDURE TO WAIT UNTIL DEVICE IS READY 				
WAIT_10_FULL:
			LD IY,(REG_STATUS)
			LD A,(IY)
			AND 10H
			JR NZ,WAIT_10_FULL
			RET
; --------------------- END | PROCEDURE TO WAIT UNTIL DEVICE IS READY 				

; --------------------- BEGIN | PROCEDURE TO WAIT AND CALL EXEC_CMD PROCEDURE 				
WAIT_10_CALL:
			PUSH HL
			LD HL,07D0H
LOOPCOUNT:	LD IY,(REG_STATUS)
			LD A,(IY)
			AND 10H
			JR Z,SAI_WAIT1
			DEC HL
			LD A,H
			OR L
			JR NZ,LOOPCOUNT
			POP HL
			RET
SAI_WAIT1:	LD A,(DE)
			LD B,A
			INC DE
			PUSH DE
			POP HL
			CALL EXEC_CMD
			POP HL
			RET
; --------------------- END | PROCEDURE TO WAIT AND CALL EXEC_CMD PROCEDURE 				

; --------------------- BEGIN | PROCEDURE TO EXECUTE COMMAND USING TOSHIBA FDC				
EXEC_CMD:	LD IY,(REG_STATUS)
			LD A,(IY)
			AND 0C0H
			CP 80H
			JR NZ,EXEC_CMD
			LD A,(HL)
			LD IY,(REG_CMD)
			LD (IY),A
			INC HL
			DJNZ EXEC_CMD
			RET
; --------------------- END | PROCEDURE TO EXECUTE COMMAND USING TOSHIBA FDC				

; --------------------- BEGIN | PROCEDURE TO READ DATA USING TOSHIBA FDC				
READ_DATA:	LD DE,(REG_STATUS)
			LD B,0
LOOPREAD0:	LD A,(DE)
			ADD A,A
			JR NC,LOOPREAD0
			ADD A,A
			RET P
			LD IY,(REG_DATA)
			LD A,(IY)
			LD (HL),A
			INC HL
			DJNZ LOOPREAD0
LOOPREAD:	LD A,(DE)
			ADD A,A
			JR NC,LOOPREAD
			ADD A,A
			RET P
			LD IY,(REG_DATA)
			LD A,(IY)
			LD (HL),A
			INC HL
			DJNZ LOOPREAD
			RET
; --------------------- END | PROCEDURE TO READ DATA USING TOSHIBA FDC				

; --------------------- BEGIN | PROCEDURE TO WRITE DATA USING TOSHIBA FDC				
WRITE_DATA:	LD DE,(REG_STATUS)
			LD B,0
LOOPWRITE0:	LD A,(DE)
			ADD A,A
			JR NC,LOOPWRITE0
			ADD A,A
			RET P
			LD A,(HL)
			LD IY,(REG_DATA)
			LD (IY),A
			INC HL
			DJNZ LOOPWRITE0
LOOPWRITE:	LD A,(DE)
			ADD A,A
			JR NC,LOOPWRITE
			ADD A,A
			RET P
			LD A,(HL)
			LD IY,(REG_DATA)
			LD (IY),A
			INC HL
			DJNZ LOOPWRITE
			RET
; --------------------- END | PROCEDURE TO WRITE DATA USING TOSHIBA FDC				
			
; --------------------- BEGIN | PROCEDURE TO READ SECTOR WITH TOSHIBA FDC
; --------------------- BEGIN | ROUTINE TO SET UP TOSHIBA FDC COMMANDS			
READ3:		LD DE,(TRACKSECTOR)
			LD BC,(DRIVELADO)
			DEC B
			LD A,B
			LD (CMD_SEEK + 2),A
			LD A,D
			LD (CMD_SEEK + 3),A
			LD A,C
			RLCA
			RLCA
			OR B
			LD (CMD_READ + 2),A
			LD (CMD_SENSE_DEV + 2),A
			LD A,D
			LD (CMD_READ + 3),A
			LD A,C
			LD (CMD_READ + 4),A
			LD A,E
			LD (CMD_READ + 5),A
			LD A,(READWRITE)
			INC A
			CPL
			AND 00000011B
			LD B,A
			LD A,(CMD_READ + 1)
			AND 11111100B
			OR B
			LD (CMD_READ + 1),A
; --------------------- END | ROUTINE TO SET UP TOSHIBA FDC COMMANDS	
			CALL LIGDRIVE ;TURN ON THE DRIVE
; --------------------- BEGIN | ROUTINE TO ???
			LD A,20H
			LD IY,(REG_CONTROL1)
			LD (IY),A
; --------------------- END | ROUTINE TO ???			
; --------------------- BEGIN | ROUTINE TO SEND THE SENSE DEVICE COMMAND
			LD HL,1388H
LOOPDEV:	DEC HL
			LD A,L
			OR H
			JR Z,CONT
			LD DE,CMD_SENSE_DEV
			CALL WAIT_10_CALL
			CALL GUARDA_CMD
			LD A,(BUFFERIX)
			BIT 5,A
			JR Z,LOOPDEV
; --------------------- END | ROUTINE TO SEND THE SENSE DEVICE COMMAND			
; --------------------- BEGIN | ROUTINE TO ???
CONT:		LD A,30H
			LD IY,(REG_CONTROL1)
			LD (IY),A
			CALL DELAY
; --------------------- END | ROUTINE TO ???			
; --------------------- BEGIN | ROUTINE TO SEND THE SEEK COMMAND
			LD DE,CMD_SEEK
			CALL WAIT_10_CALL
			CALL WAIT_10_FULL
; --------------------- END | ROUTINE TO SEND THE SEEK COMMAND			
; --------------------- BEGIN | ROUTINE TO SEND THE SENSE INTERRUPT COMMAND
LOOPINT:	LD DE,CMD_SENSE_INT
			CALL WAIT_10_CALL
			CALL GUARDA_CMD
			LD A,(BUFFERIX)
			BIT 5,A
			JR Z,LOOPINT
; --------------------- END | ROUTINE TO SEND THE SENSE INTERRUPT COMMAND			
; --------------------- BEGIN | ROUTINE TO DELAY
			LD BC,0773H
LOOPDL:		DEC BC
			LD A,B
			OR C
			JR NZ,LOOPDL
; --------------------- END | ROUTINE TO DELAY			
; --------------------- BEGIN | ROUTINE TO ???
			LD A,20H
			LD IY,(REG_CONTROL1)
			LD (IY),A
; --------------------- END | ROUTINE TO ???			
; --------------------- BEGIN | ROUTINE TO READ/WRITE SECTOR
			LD DE,CMD_READ
			CALL WAIT_10_CALL
			LD HL,BUFFERADDR
			LD A,(READWRITE)
			AND A
			JR Z,RDATA
			CALL WRITE_DATA
			JR CONTOPC
RDATA:		CALL READ_DATA
; --------------------- END | ROUTINE TO READ/WRITE SECTOR
; --------------------- BEGIN | ROUTINE TO ????	
CONTOPC:	LD A,2
			LD IY,(REG_CONTROL1)
			LD (IY),A
			CALL DELAY
			INC A
			LD (IY),A
			DEC A
			LD (IY),A
			CALL GUARDA_CMD			
			LD A,30H
			LD IY,(REG_CONTROL1)
			LD (IY),A
; --------------------- END | ROUTINE TO ????			
			A,4 ; TURN ON THE DRIVE
			LD IY,(REG_CONTROL0)
			LD (IY),A	
			JP SAIDA	
; --------------------- END | PROCEDURE TO READ SECTOR WITH TOSHIBA FDC		
; ---------------------
; --------------------- END | PROCEDURES TO ACCESS TOSHIBA FDC

; --------------------- BEGIN | AUXILIARY PROCEDURES
; ---------------------
; --------------------- BEGIN | PROCEDURES TO DELAY	
DELAY2:		PUSH BC
			LD B,30H
LOOPDELAY2:	EX (SP),HL
			EX (SP),HL
			DJNZ LOOPDELAY2
			POP BC
			RET
			
DELAY3:		PUSH BC
			LD B,80H
LOOPDELAY3:	EX (SP),HL
			EX (SP),HL
			DJNZ LOOPDELAY3
			POP BC
			RET			
; --------------------- BEGIN | PROCEDURES TO DELAY	

; --------------------- BEGIN | PROCEDURE TO SET DRIVE AND SIDE
SETDRIVELADO:
			PUSH AF
			PUSH HL
			PUSH DE
			PUSH BC
			LD B,A
			RRCA
			RRCA
			RRCA
			RRCA
			AND 1
			LD L,A
			LD A,B
			AND 3
			LD H,A
			LD (DRIVELADO),HL
			POP BC
			POP DE
			POP HL
			POP AF
			RET
; --------------------- END | PROCEDURE TO SET DRIVE AND SIDE			
			
; --------------------- BEGIN | PROCEDURE TO SEARCH FOR BYTES IN MEMORY USING MASK			
SEARCH_BYTES:
			LD IX,4000H
			LD HL,4000H
			LD A,(IY)
			LD B,A
			LD E,A
			LD D,0
			INC IY
			INC A
SEARCHLOOP:	LD (BYTEIY2 + 2),A
			LD A,D
			LD (BYTEIX + 2),A
			LD (BYTEIY + 2),A
BYTEIX:		LD A,(IX + 0)
BYTEIY2:	AND (IY + 0)
BYTEIY:		CP (IY + 0)
			JR NZ,SEARCHNOT
			INC D
			LD A,E
			INC A
			ADD A,D
			DJNZ SEARCHLOOP
			LD A,1
			RET
SEARCHNOT:	DEC HL
			LD A,L
			OR H
			JR Z,SEARCHSAI
			LD D,0
			INC IX
			LD A,E
			LD B,A
			INC A
			JR SEARCHLOOP
SEARCHSAI:	XOR A
			RET	
; --------------------- END | PROCEDURE TO SEARCH FOR BYTES IN MEMORY USING MASK				
	
; --------------------- BEGIN | PROCEDURES TO IDENTIFY THE FDC CHIPSET
GETINTERFACE:
			LD IY,STR_PHILIPS
			CALL SEARCH_BYTES
			AND A
			JR Z,SEARCHNF00
			LD A,1
			LD (INTERFACE),A
			LD A,(IX + 4)
			LD L,A
			LD A,(IX + 5)
			LD H,A
			LD (REG_STATUS_2),HL
			LD A,(IX + 11)
			LD L,A
			LD A,(IX + 12)
			LD H,A
			LD (REG_CMD),HL
			INC HL
			LD (REG_TRACK),HL
			INC HL
			LD (REG_SECTOR),HL
			INC HL
			LD (REG_DATA),HL
			INC HL
			LD (REG_SIDE),HL
			INC HL
			LD (REG_DRIVE),HL
			JP SEARCHEND
SEARCHNF00:	LD IY,STR_PHILIPS1
			CALL SEARCH_BYTES	
			AND A
			JP Z,SEARCHEND0A	
			LD A,1
			LD (INTERFACE),A
			LD A,(IX + 8)
			LD L,A
			LD A,(IX + 9)
			LD H,A
			LD (REG_STATUS_2),HL
			LD A,(IX + 11)
			LD L,A
			LD A,(IX + 12)
			LD H,A
			LD (REG_CMD),HL
			INC HL
			LD (REG_TRACK),HL
			INC HL
			LD (REG_SECTOR),HL
			INC HL
			LD (REG_DATA),HL
			INC HL
			LD (REG_SIDE),HL
			INC HL
			LD (REG_DRIVE),HL
			JP SEARCHEND	
SEARCHEND0A:LD IY,STR_PHIL_NAT
			CALL SEARCH_BYTES	
			AND A
			JP Z,SEARCHEND00	
			LD A,1
			LD (INTERFACE),A
			LD A,(IX + 4)
			LD L,A
			LD A,(IX + 5)
			LD H,A
			LD (REG_STATUS_2),HL
			LD A,(IX + 15)
			LD L,A
			LD A,(IX + 16)
			LD H,A
			LD (REG_CMD),HL
			INC HL
			LD (REG_TRACK),HL
			INC HL
			LD (REG_SECTOR),HL
			INC HL
			LD (REG_DATA),HL
			INC HL
			LD (REG_SIDE),HL
			INC HL
			LD (REG_DRIVE),HL
			LD HL,(REG_STATUS_2)
			LD DE,(REG_SIDE)
			LD A,H
			CP D
			JP NZ,SEARCHEND
			LD A,L
			CP E
			JP NZ,SEARCHEND
			LD A,2
			LD (INTERFACE),A
			LD (REG_DRIVE),DE
			JP SEARCHEND				
SEARCHEND00:LD IY,STR_PHILIPS2
			CALL SEARCH_BYTES	
			AND A
			JP NZ,SEARCHEND0
SEARCHNF1:	LD IY,STR_NATIONAL
			CALL SEARCH_BYTES
			AND A
			JR Z,SEARCHNF2
			LD A,2
			LD (INTERFACE),A
			LD A,(IX + 1)
			LD L,A
			LD A,(IX + 2)
			LD H,A
			LD (REG_CMD),HL
			INC HL
			LD (REG_TRACK),HL
			INC HL
			LD (REG_SECTOR),HL
			INC HL
			LD (REG_DATA),HL
			INC HL
			LD (REG_DRIVE),HL
			LD (REG_STATUS_2),HL
			JP SEARCHEND
SEARCHNF2:	LD IY,STR_TOSHIBA1
			CALL SEARCH_BYTES
			AND A
			JR Z,SEARCHNF3
			LD A,3
			LD (INTERFACE),A	
			LD A,(IX + 1)
			LD L,A
			LD A,(IX + 2)
			LD H,A
			LD (REG_STATUS),HL
			LD A,(IX + 11)
			LD L,A
			LD A,(IX + 12)
			LD H,A
			LD (REG_CMD),HL
			LD (REG_RESULT_STATUS),HL
			LD (REG_DATA),HL
			LD IY,STR_TOSHIBA2
			CALL SEARCH_BYTES
			LD A,(IX + 3)
			LD L,A
			LD A,(IX + 4)
			LD H,A
			LD (REG_CONTROL1),HL
			LD IY,STR_TOSHIBA3
			CALL SEARCH_BYTES
			LD A,(IX + 3)
			LD L,A
			LD A,(IX + 4)
			LD H,A
			LD (REG_CONTROL0),HL
			JP SEARCHEND
SEARCHNF3:	LD IY,STR_MICROSOL
			CALL SEARCH_BYTES
			AND A
			JR Z,SEARCHNF4
			LD A,4
			LD (INTERFACE),A
			JP SEARCHEND
SEARCHEND0: LD HL,07FFFH
			LD (REG_STATUS_2),HL
			LD A,1
			LD (INTERFACE),A
			JP SEARCHEND0B
SEARCHNF4:	XOR A
			LD (INTERFACE),A
SEARCHEND0B:LD HL,07FFDH
			LD (REG_DRIVE),HL
			DEC HL
			LD (REG_SIDE),HL
			DEC HL
			LD (REG_DATA),HL
			DEC HL
			LD (REG_SECTOR),HL
			DEC HL
			LD (REG_TRACK),HL
			DEC HL
			LD (REG_CMD),HL
SEARCHEND:	LD A,(INTERFACE)
			AND A
			RET Z
			CALL GETINTREG
			LD HL,REG_ADDR
			CALL AJUSTAREG
			LD HL,(REG_STATUS_2)
			CALL SAVEREG
			LD HL,(REG_DRIVE)
			CALL SAVEREG
			LD HL,(REG_SIDE)
			CALL SAVEREG
			LD HL,(REG_DATA)
			CALL SAVEREG
			LD HL,(REG_SECTOR)
			CALL SAVEREG
			LD HL,(REG_TRACK)
			CALL SAVEREG
			LD HL,(REG_CMD)
			CALL SAVEREG
			LD HL,(REG_STATUS)
			CALL SAVEREG
			LD HL,(REG_RESULT_STATUS)
			CALL SAVEREG
			LD HL,(REG_CONTROL0)
			CALL SAVEREG
			LD HL,(REG_CONTROL1)
			CALL SAVEREG
			RET
			
GETINTREG:	LD HL,(INTERDISCO)
			LD C,0
			LD A,C
			OR H
			LD C,A
			LD A,L
			AND 00000011B
			RLCA
			RLCA
			OR C
			LD C,A
			LD A,L
			AND 10000000B
			OR C
GETINTREG2:	LD C,A
			LD B,4
			LD D,1
			LD HL,0FB22H
GETINTRLP:	LD A,(HL)
			CP C
			JR Z,GETINTRSAI
			INC HL
			INC HL
			INC D
			DJNZ GETINTRLP
GETINTRSAI: LD A,D	
			RET
						
SETREG:		LD A,(INTERFACE)
			AND A
			RET Z
			CP 0FFH
			RET Z
			LD A,(INTERDISK)
			CALL GETINTREG2
			LD HL,REG_ADDR
			CALL AJUSTAREG
			CALL LOADREG
			LD (REG_STATUS_2),HL
			CALL LOADREG
			LD (REG_DRIVE),HL		
			CALL LOADREG
			LD (REG_SIDE),HL		
			CALL LOADREG
			LD (REG_DATA),HL			
			CALL LOADREG
			LD (REG_SECTOR),HL		
			CALL LOADREG
			LD (REG_TRACK),HL			
			CALL LOADREG
			LD (REG_CMD),HL		
			CALL LOADREG
			LD (REG_STATUS),HL			
			CALL LOADREG
			LD (REG_RESULT_STATUS),HL		
			CALL LOADREG
			LD (REG_CONTROL0),HL		
			CALL LOADREG	
			LD (REG_CONTROL1),HL
			RET

LOADREG:	LD A,(DE)
			LD L,A
			INC DE
			LD A,(DE)
			LD H,A
			INC DE
			RET
			
SAVEREG:	LD A,L
			LD (DE),A
			INC DE
			LD A,H
			LD (DE),A
			INC DE
			RET
			
AJUSTAREG:	LD B,A
			LD DE,22
AJUSTABACK:	DEC B
			LD A,B
			AND A
			JR Z,AJUSTASAI
			ADD HL,DE
			JR AJUSTABACK 
AJUSTASAI:	PUSH HL
			POP DE
			RET
; --------------------- END | PROCEDURES TO IDENTIFY THE FDC CHIPSET					

; --------------------- BEGIN | PROCEDURE TO READ SECTOR USING BDOS		
R144H:		PUSH AF
			PUSH DE
			PUSH BC	
			LD A,(OPERAC)
			AND A
			JR Z,R144LE
			LD HL,(ADDR)
			LD DE,DMA
			LD BC,200H
			LDIR
R144LE:		LD DE,DMA
			LD C,1AH
			CALL BDOS
			POP HL
			POP DE
			POP AF
			LD L,A
			JR C,R144GRAVA
			LD C,2FH
			CALL BDOS
			LD HL,DMA
			LD DE,(ADDR)
			LD BC,200H
			LDIR	
			RET
R144GRAVA:	LD C,30H
			CALL BDOS
			RET
; --------------------- END | PROCEDURE TO READ SECTOR USING BDOS	

; --------------------- BEGIN | PROCEDURE TO SET RAM IN PAGE 1 (4000h)		
SETRAM:		LD A,(0F342H)
			LD HL,4000H
			CALL 24H
			LD A,(0F343H)
			LD HL,8000H
			CALL 24H
			RET	
; --------------------- END | PROCEDURE TO SET RAM IN PAGE 1 (4000h)				

; --------------------- BEGIN | PROCEDURE TO DISPLAY 6 DECIMAL NUMBERS
; --------------------- Created by: Rudolf Gutlich @2014/04/18
PRINTSIX:	PUSH HL
			LD (ADDRVRAM),DE
			LD HL,BUFNUM
			LD (DIGADDR),HL
			LD HL,0
			LD (REG32),HL
			POP HL
			LD (REG32 + 2),HL		
; --------------------- BEGIN | ROUTINE TO MULTIPLY BLOCK NUMBER BY 128
			LD HL,(REG32 + 2)
			LD B,0
			LD E,0
			SRL H; shift MSB right 1 bit, ignoring carry
			LD C,H; put result in C
			RR L; shift LSB right 1 bit, using carry
			LD D,L; put result in D
			RR E; put carry on bit 7 of E

			LD (REG32),BC
			LD (REG32 + 2),DE
; --------------------- END | ROUTINE TO MULTIPLY BLOCK NUMBER BY 128
; --------------------- BEGIN | MAIN ROUTINE
			PUSH BC
			PUSH DE
			POP HL
			POP DE
			CALL PRINT24
			LD HL,BUFEND-6
			LD DE,(ADDRVRAM)
			LD BC,6			
			CALL WVRAM	
			RET
; --------------------- END | MAIN ROUTINE    
; --------------------- BEGIN | ROUTINE TO CONVERT HEX TO DECIMAL 
PRINT24:	LD D,0
			LD BC,0
			LD IX,0          
			LD (BCDBUF),HL
			LD (BCDBUF+2),DE
			LD (BCDBUF+4),BC
			LD (BCDBUF+6),IX 
			LD HL,BUFNUM
			LD DE,BUFNUM+1
			LD (HL),"0"
			LD BC,18
			LDIR            
			LD (BUFEND-1),BC 
			LD E,1          
			LD HL,BCDBUF+8 
			LD BC,0909h
			XOR A
BCDSKP0:	DEC B
			JR Z,BCDSIZE
			DEC HL
			OR (HL) 
			JR Z,BCDSKP0
BCDFND1: 	DEC C
			RLA
			JR NC,BCDFND1 
			RRA
			LD D,A 
BCDLUS2: 	PUSH HL
			PUSH BC
BCDLUS1: 	LD HL,BUFEND-1 
			LD B,E
			RL D 
BCDLUS0: 	LD A,(HL)
			ADC A,A
			DAA
			LD (HL),A 
			DEC HL
			DJNZ BCDLUS0 
			JR NC,BCDNEXT
			INC E 
			LD (HL),1 
BCDNEXT:  	DEC C
			JR NZ,BCDLUS1
			POP BC 
			LD C,8
			POP HL
			DEC HL
			LD D,(HL)
			DJNZ BCDLUS2
BCDSIZE: 	LD HL,BUFEND
			LD C,E 
			OR A
			SBC HL,BC 
			LD D,H
			LD E,L
			SBC HL,BC
			EX DE,HL
			LD B,C  
			SLA C 
			LD A,"0"
			RLD
			CP "0" 
			JR NZ,BCDEXPH
			DEC C 
			INC DE
			JR BCDEXPL 
BCDEXP:  	RLD 
BCDEXPH: 	LD (DE),A 
			INC DE
BCDEXPL: 	RLD
			LD (DE),A
			INC DE
			INC HL 
			DJNZ BCDEXP 
			SBC HL,BC 
			RET
; --------------------- END | ROUTINE TO CONVERT HEX TO DECIMAL 
; --------------------- END | PROCEDURE TO DISPLAY 6 DECIMAL NUMBERS

; --------------------- END | PROCEDURE TO DISPLAY PROGRESS BAR
PROGRESSBAR:PUSH AF
			PUSH HL
			PUSH DE
			PUSH BC
			LD A,(BARRAFLAG)
			AND A
			JR NZ,PBARPULA
			LD A,1
			LD (BARRAFLAG),A
			LD A,(BARRAPOS)
			LD B,A
			DEC B
			DEC B
			LD A,0DCH
			LD HL,(BARRADDR)
			CALL WVBYTE
PBARBACK:	INC HL
			LD A,0C2H
			CALL WVBYTE
			DJNZ PBARBACK
			INC HL
			LD A,0DBH
			CALL WVBYTE
PBARPULA:	LD DE,(BARRAVALUE)
			LD A,(BARRAPOS)
			CALL MULT12
			PUSH HL
			POP BC
			LD DE,(BARRAMAX)
			CALL DIV16
			LD B,C
			LD A,B
			AND A
			JR Z,PBARSAI
			LD A,(BARRATUAL)
			CP B
			JR Z,PBARSAI
			LD A,B
			LD (BARRATUAL),A
			LD HL,(BARRADDR)
PBARBACK2:	LD A,0C0H
			CALL WVBYTE
			INC HL
			DJNZ PBARBACK2
PBARSAI:	POP BC
			POP DE
			POP HL
			POP AF
			RET
; --------------------- END | PROCEDURE TO DISPLAY PROGRESS BAR			

; --------------------- BEGIN | PROCEDURE TO INDENTIFY INTERFACES
IDENTINTERFACES:
			LD DE,MSGFDC
			LD HL,442
			CALL PRINTH 			
			LD HL,INTERFACES
			LD (INTERFACEADDR),HL
			LD B,4
			LD A,0FFH
LIMPAINTER:	LD (HL),A
			INC HL
			DJNZ LIMPAINTER
			LD HL,INTERFACESLOTS
			LD (INTERFACESLOTADDR),HL
			XOR A
			LD (QTDINTERFACES),A
			LD B,4
			LD HL,0FB22H
			LD (INTERFACEADDRDOS),HL
INTERLOOP:	LD HL,(INTERFACEADDRDOS)
			LD A,(HL)
			INC HL
			INC HL
			LD (INTERFACEADDRDOS),HL
			AND A
			JR Z,INTERSAI
			AND 10001111B
			LD E,A
			AND 3
			LD (INTERDISCO + 1),A
			LD A,E
			AND 00001100B
			RRCA
			RRCA
			LD C,A
			LD A,E
			AND 10000000B
			OR C
			LD (INTERDISCO),A
			PUSH BC
			LD B,0
			CALL CHAVEIA
			LD A,(INTERFACE)
			LD HL,(INTERFACEADDR)
			LD (HL),A
			INC HL
			LD (INTERFACEADDR),HL
			LD IX,INTERDISCO
			LD IY,(INTERFACESLOTADDR)
			LD A,(IX)
			LD (IY),A
			LD A,(IX + 1)
			LD (IY + 1),A
			INC IY
			INC IY
			LD (INTERFACESLOTADDR),IY
			LD A,(QTDINTERFACES)
			INC A
			LD (QTDINTERFACES),A
			POP BC
			DJNZ INTERLOOP
INTERSAI:	RET
; --------------------- END | PROCEDURE TO INDENTIFY INTERFACES

; --------------------- BEGIN | PROCEDURE TO PRINT SLOT/SUBSLOT
PRINTSLOT:	PUSH IX
			PUSH HL
			PUSH BC
			PUSH DE
			PUSH HL
			LD DE,MSGSLOT
			CALL PRINTH
			POP HL
			LD DE,6
			ADD HL,DE
			POP DE
			LD A,D
			ADD A,30H
			PUSH DE
			PUSH HL
			CALL WVBYTE
			POP HL
			INC HL
			PUSH HL
			LD A,'.'
			CALL WVBYTE
			POP HL
			INC HL
			POP DE
			LD A,E
			AND 3
			ADD A,30H
			CALL WVBYTE
			POP BC
			POP HL
			POP IX
			RET		
; --------------------- END | PROCEDURE TO PRINT SLOT/SUBSLOT

; --------------------- BEGIN | PROCEDURE TO CHOOSE INTERFACE
CHOOSEINTERFACE:
			LD A,(QTDINTERFACES)
			LD B,A
			LD IX,INTERFACESLOTS
			LD HL,328
			LD A,1
			LD (CHOOSEPOSITION),A
CHOOSELOOP:	LD A,(CHOOSEPOSITION)
			CALL PRINTOPTIONS
CHOOSEVAR1:	LD A,(IX + 0)
			LD E,A
CHOOSEVAR2:	LD A,(IX + 1)
			LD D,A
			PUSH DE
			LD DE,14
			ADD HL,DE
			POP DE
			CALL PRINTSLOT
			LD DE,26
			ADD HL,DE
			LD A,(CHOOSEVAR1 + 2)
			INC A
			INC A
			LD (CHOOSEVAR1 + 2),A
			LD A,(CHOOSEVAR2 + 2)
			INC A
			INC A
			LD (CHOOSEVAR2 + 2),A
			LD A,(CHOOSEPOSITION)
			INC A
			LD (CHOOSEPOSITION),A
			DJNZ CHOOSELOOP
			LD A,(QTDINTERFACES)
			CP 1
			JR Z,CHOOSE1FDC
			CALL CHGET
			SUB 31H
			LD (CHOOSEPOSITION),A
			LD IX,INTERFACES
			LD (CHOOSEVAR3 + 2),A
CHOOSEVAR3:	LD A,(IX + 0)
			LD (INTERFACE),A
			LD IX,INTERFACESLOTS	
			LD A,(CHOOSEPOSITION)
			ADD A,A
			LD (CHOOSEVAR4 + 2),A
			INC A
			LD (CHOOSEVAR5 + 2),A
CHOOSEVAR4:	LD A,(IX + 0)
			LD L,A
CHOOSEVAR5:	LD A,(IX + 1)		
			LD H,A
CHOOSESAI:	LD (INTERDISCO),HL
			LD C,0
			LD A,L
			AND 80H
			OR C
			LD C,A
			LD A,L
			AND 3
			RLCA
			RLCA
			OR C
			LD C,A
			LD A,H
			OR C
			LD (INTERDISK),A
			LD (DEBUG_SLOT),HL
			LD A,(INTERFACE)
			LD (DEBUG_SLOT + 2),A
			XOR A
			LD (CHOOSEVAR1 + 2),A
			INC A
			LD (CHOOSEVAR2 + 2),A
			RET	
CHOOSE1FDC:	LD HL,(INTERFACESLOTS)
			LD A,(INTERFACES)
			LD (INTERFACE),A
			JR CHOOSESAI
; --------------------- END | PROCEDURE TO CHOOSE INTERFACE

; --------------------- BEGIN | PROCEDURE TO PRINT OPTIONS
PRINTOPTIONS:
			PUSH IX
			PUSH BC
			PUSH HL
			PUSH AF
			PUSH HL
			ADD A,30H
			CALL WVBYTE
			POP HL
			INC HL
			PUSH HL
			LD A,')'
			CALL WVBYTE
			POP HL
			INC HL
			INC HL
			POP AF
			DEC A
			PUSH HL
			LD HL,INTERFACES
			LD DE,0
			LD E,A
			ADD HL,DE
			LD A,(HL)
			AND A
			JR Z,POMSG0
			CP 1
			JR Z,POMSG1
			CP 2
			JR Z,POMSG2
			CP 3
			JR Z,POMSG3
			LD DE,MSGFDC4
			JR POCONT
POMSG0:		LD DE,MSGFDC0
			JR POCONT
POMSG1:		LD DE,MSGFDC1
			JR POCONT
POMSG2:		LD DE,MSGFDC2
			JR POCONT
POMSG3:		LD DE,MSGFDC3
POCONT:		POP HL
			CALL PRINTH
			POP HL
			POP BC
			POP IX
			RET			
; --------------------- END | PROCEDURE TO PRINT OPTIONS
; ---------------------
; --------------------- END | AUXILIARY PROCEDURES     

ENDOFCODE: 


	END     START










