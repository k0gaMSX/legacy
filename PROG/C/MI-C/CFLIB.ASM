;MI - C  v-3.18   (C) COPYRIGHT  G. KERSTING / H. ROSE, 1983
;
; *****************************************************
; BCD - FLOATING-POINT PACKAGE
; FORMAT: 8 BYTE: 0 .. 6 MANTISSE 
; BYTE 7 IST BINAERER EXPONENT ZUR BASIS 10 
;
; ****************************************************
; RUNTIME LIBRARY   MI - C   V  3.1   
;
; ENTRY CFPUTM,CRPUTM,CFSPUTM,CRSPUTM
; ENTRY CFGDIR,CFSGDIR
; ENTRY CFGPIND,CRGPIND,CFSGPIND,CRSGPIND
; ENTRY CFGSIND,CRGSIND,CFSGSIND,CRSGSIND
; ENTRY CRPIND,CRSPIND,CFPIND,CFSPIND
; ENTRY CFPUSH,CFSPUSH,CFPOP,CFSPOP
; ENTRY CFINC,CFSINC,CFDEC,CFSDEC
; ENTRY CFEQ,CFNE,CFGT,CFLE,CFGE,CFLT
; ENTRY CFNEG,CFSNEG,CFLNOT,CFSLNOT
; ENTRY CFPRIM,CFSEC
; ENTRY CFSHL
; ENTRY CFSWAP,CFSFLAG,CFSSFLAG,CFSWTEST
; EXT CC0DIV
; EXT CLPRIM,CLSEC
;
; ENTRY CFADD,CFSUB
; ENTRY CFMULT,CFDIV
; ENTRY CFSSUB,CFSDIV
;
;
;
;
; CF0DIV WIRD AUFGERUFEN, WENN EINE DIVISION DURCH 0 
; AUFTRITT. EINE ANDERE REAKTION KANN VOM BENUTZER 
; EINGEFUEGT WERDEN. 
;
CF0DIV:
	JMP CC0DIV
;
;	PUSH PSW
;	PUSH B
;	PUSH D
;	PUSH H
;
;	LXI D,CF0TEXT
;	MVI C,9
;	CALL 5		; BDOS AUFRUF TEXTAUSGABE KONSOLE
;
;	POP H
;	POP D
;	POP B
;	POP PSW
;	JMP 0
;
; ALTERNATIVES ENDE STATT JMP 0
;
;	RET
;
;CF0TEXT:
; DB 0DH,0AH,'0 - DIVISION',0DH,0AH,'$'
;
;
;
;
; CFUNDERFLOW  WIRD AUFGERUFEN, WENN EINE GLEITKOMMAZAHL VOM
; BETRAG ZU KLEIN WIRD.  
; EINE ANDERE REAKTION KANN VOM BENUTZER 
; EINGEFUEGT WERDEN. 
;
CFUNDERFLOW:
	PUSH PSW
	PUSH B
	PUSH D
	PUSH H
;
	LXI D,CFUNTEXT
	MVI C,9
	CALL 5		; BDOS AUFRUF TEXTAUSGABE KONSOLE
;
	POP H
	POP D
	POP B
	POP PSW
	RET
;
; ALTERNATIVES ENDE STATT RET
;
;	JMP 0
;
CFUNTEXT:
 DB 0DH,0AH,'UNDERFLOW',0DH,0AH,'$'
;
;
;
; CFOVERFLOW  WIRD AUFGERUFEN, WENN EINE GLEITKOMMAZAHL VOM
; BETRAG ZU GROSS WIRD.  
; EINE ANDERE REAKTION KANN VOM BENUTZER 
; EINGEFUEGT WERDEN. 
;
CFOVERFLOW:
	PUSH PSW
	PUSH B
	PUSH D
	PUSH H
;
	LXI D,CFOVTEXT
	MVI C,9
	CALL 5		; BDOS AUFRUF TEXTAUSGABE KONSOLE
;
	POP H
	POP D
	POP B
	POP PSW
	RET
;
; ALTERNATIVES ENDE STATT RET
;
;	JMP 0
;
CFOVTEXT:
 DB 0DH,0AH,'OVERFLOW',0DH,0AH,'$'
;
;
DMANT	EQU 7	;BYTE ANZAHL DER MANTISSE
VORZEI	EQU DMANT-1
EXPONENT	EQU DMANT
RMANT	EQU	DMANT+DMANT;
CFLOANF EQU 4
;
;	DSEG
CFSEC:		DS 8
CFPRIM:		DS 8
;
;	CSEG
;
CFPIND:
	MOV	B,D
	MOV	C,E
CFPUTM:
	LXI	H,CFPRIM
CFP1:
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	RET
CRPIND:
	MOV	B,D
	MOV	C,E
CRPUTM:
	LDA	CFPRIM+EXPONENT
	ANA	A
	JNZ	CRP0
	INX	B
	INX	B
	INX	B
	STAX	B
	RET
CRP0:
	LXI	H,CFPRIM+CFLOANF-1
	LDA	CFPRIM+VORZEI
CFRMOV:
	ANI	0F0H
	MOV	A,M	
	JZ	CFRM1	
	CPI	50H
	JNZ	CFRM1	
	DCX	H
	MOV	A,M
	DCX	H
	ORA	M
	DCX	H
	ORA	M
	INX	H
	INX	H
	INX	H
	JZ	CFRM2	
	MOV	A,M
CFRM1:
	ADI	50H
	DAA
	MVI	A,0
CFRM2:
	INX	H
	ADC	M
	DAA
	STAX	B
	INX	H
	INX	B
	MVI	A,0
	ADC	M
	DAA
	STAX	B
	INX	H
	INX	B
	MVI	A,0
	ADC	M
	DAA
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	DCX	B
	LDAX	B
	ANI	0FH
	MOV	L,A
	DCX	B
	LDAX	B
	ORA	L
	MOV	L,A
	DCX	B
	LDAX	B
	ORA	L
	RNZ
	INX	B
	INX	B
	LDAX	B
	CPI	10H
	MVI	A,1	
	JZ	CFRM1V
	MVI	A,99H
CFRM1V:
	STAX	B
	INX	B
	LDAX	B
	INR	A
	JZ	CFRM2V
	STAX	B
	RET
CFRM2V:
	CALL	CFOVERFLOW
	DCX	B
	LDAX	B
	ANI	0F0H
	JNZ	CFRM3V
	MVI	A,9
	STAX	B
	DCX	B
	MVI	A,99H
	STAX	B
	DCX	B
	STAX	B
	RET
CFRM3V:
	MVI	A,90H
	STAX	B
	DCX	B
	DCX	B
	MVI	A,1
	STAX	B
	RET
CFSPIND:
	MOV	B,H
	MOV	C,L
CFSPUTM:
	XCHG
	LXI	H,CFSEC
CFSP1:
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	XCHG
	RET
CRSPIND:
	MOV	B,H
	MOV	C,L
CRSPUTM:
	XCHG
	LDA	CFSEC+EXPONENT
	ANA	A
	JNZ	CRSP0
	INX	B
	INX	B
	INX	B
	STAX	B
	XCHG
	RET
CRSP0:
	LXI	H,CFSEC+CFLOANF-1
	LDA	CFSEC+VORZEI
	CALL	CFRMOV
	XCHG
	RET
CFGDIR:
	POP	H
	MVI	B,8
	PUSH	D	
	LXI	D,CFPRIM
CFGD0:
	MOV	A,M
	STAX	D
	INX	D
	INX	H
	DCR	B
	JNZ	CFGD0
	POP	D
	PCHL
CFSGDIR:
	XTHL
	MVI	B,8
	LXI	D,CFSEC
CFSGD0:
	MOV	A,M
	STAX	D
	INX	D
	INX	H
	DCR	B
	JNZ	CFSGD0
	XTHL
	RET
CFGPIND:
	LXI	B,CFPRIM
CFMOV8:
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	RET
CRGPIND:
	LXI	B,CFPRIM
CFM04:
	XRA	A
	STAX	B
	INX	B
	STAX	B
	INX	B
	STAX	B
	INX	B
	STAX	B
	INX	B
CFMOV4:
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	INX	H
	INX	B
	MOV	A,M
	STAX	B
	RET
CFSGPIND:
	LXI	B,CFSEC
	JMP	CFMOV8
CRSGPIND:
	LXI	B,CFSEC
	JMP	CFM04
CFGSIND:
	LXI	B,CFPRIM
	JMP	CFSMV8
CRGSIND:
	LXI	B,CFPRIM
	JMP	CFSM04
CFSGSIND:
	LXI	B,CFSEC
CFSMV8:
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	RET
CRSGSIND:
	LXI	B,CFSEC
CFSM04:
	XRA	A
	STAX	B
	INX	B
	STAX	B
	INX	B
	STAX	B
	INX	B
	STAX	B
	INX	B
CFSMV4:
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	INX	D
	INX	B
	LDAX	D
	STAX	B
	RET
CFPUSH:
	LHLD	CFPRIM+VORZEI
	POP	B
	PUSH	H
	LHLD	CFPRIM+4
	PUSH	H
	LHLD	CFPRIM+2
	PUSH	H
	LHLD	CFPRIM
	PUSH	H
	PUSH	B
	RET
CFSPUSH:
	XCHG
	POP	B
	LHLD	CFSEC+VORZEI
	PUSH	H
	LHLD	CFSEC+4
	PUSH	H
	LHLD	CFSEC+2
	PUSH	H
	LHLD	CFSEC
	PUSH	H
	XCHG
	PUSH	B
	RET
CFPOP:
	POP	B
	POP	H
	SHLD	CFPRIM
	POP	H
	SHLD	CFPRIM+2
	POP	H
	SHLD	CFPRIM+4
	POP	H
	SHLD	CFPRIM+VORZEI
	PUSH	B
	RET
CFSPOP:
	XCHG
	POP	B
	POP	H
	SHLD	CFSEC
	POP	H
	SHLD	CFSEC+2
	POP	H
	SHLD	CFSEC+4
	POP	H
	SHLD	CFSEC+VORZEI
	XCHG
	PUSH	B
	RET
CFINC:
	LDA	CFPRIM+EXPONENT
	ANA	A
	JNZ	CFIP1
	LXI	H,CFPRIM
	MVI	C,1
	JMP	CFXNUL
CFIP1:
	PUSH	D
	CALL	CFSPUSH
	LXI	H,CFSEC
	MVI	C,1
	CALL	CFXNUL
	CALL	CFADD
	CALL	CFSPOP   
	POP	D
	RET
CFSINC:
	PUSH	H
	LDA	CFSEC+EXPONENT
	ANA	A
	JNZ	CFSI1
	LXI	H,CFSEC
	MVI	C,1
	CALL	CFXNUL
	POP	H
	RET
CFSI1:
	CALL	CFPUSH
	LXI	H,CFPRIM
	MVI	C,1
	CALL	CFXNUL
	CALL	CFADD
	CALL	CFSWAP
	CALL	CFPOP
	POP	H
	RET
CFXNUL:
	XRA	A
CFXP0:
	MOV	M,A
	INX	H
	MOV	M,A
	INX	H
	MOV	M,A
	INX	H
	MOV	M,A
	INX	H
	MOV	M,A
	INX	H
	MOV	M,A
	INX	H
	MOV	M,C
	INX	H
	MVI	M,81H
	RET
CFDEC:
	LDA	CFPRIM+EXPONENT
	ANA	A
	JNZ	CFDP1
	LXI	H,CFPRIM
	MVI	C,99H
	JMP	CFXNUL
	RET
CFDP1:
	PUSH	D
	CALL	CFSPUSH
	LXI	H,CFSEC
	MVI	C,99H
	CALL	CFXNUL
	CALL	CFADD
	CALL	CFSPOP
	POP	D
	RET
CFSDEC:
	LDA	CFSEC+EXPONENT
	ANA	A
	JNZ	CFSDP1
	LXI	H,CFSEC
	MVI	C,99H
	CALL	CFXNUL
	RET
CFSDP1:
	CALL	CFPUSH
	LXI	H,CFPRIM
	MVI	C,99H
	CALL	CFXNUL
	CALL	CFADD
	CALL	CFSWAP
	CALL	CFPOP
	POP	H
	RET
CFSWAP:
	MVI	B,8
	PUSH	H
	PUSH	D
	LXI	H,CFPRIM+EXPONENT
	LXI	D,CFSEC+EXPONENT
CFSW1:
	LDAX	D
	MOV	C,A
	MOV	A,M
	STAX	D
	MOV	A,C
	MOV	M,A
	DCX	H
	DCX	D
	DCR	B
	JNZ	CFSW1
	LHLD	CLPRIM
	XCHG
	LHLD	CLSEC
	SHLD	CLPRIM
	XCHG
	SHLD	CLSEC
	POP	H
	POP	D
	RET	
CFEQ:	
	CALL CFCMP
	LXI H,1
	JNZ CFEQ1
	ORA	L
	RET
CFEQ1:
	DCR	L
	RET
CFNE:	
	CALL	CFCMP
	LXI H,1
	RNZ
	DCR	L
	RET
CFGT:
	CALL CFCMP
	LXI	H,0
	RZ
	JC	CFGT1
	INR	L
	RET
CFGT1:
	XRA	A
	RET
CFLE:	
	CALL CFCMP
	LXI	H,1
	RC
	JZ	CFLE1
	DCR	L
	RET
CFLE1:
	ORA	L
	RET
CFGE:
	CALL CFCMP
	LXI	H,0
	INR	L
	RNC
	DCR	L
	RET
CFLT:	
	CALL CFCMP
	LXI	H,1
	RC
	DCR	L
	RET
CFSWTEST:
CFCMP:
	LXI	H,CFSEC+VORZEI
	LXI	D,CFPRIM+VORZEI
	LDAX	D
	ANI	0F0H
	MOV	C,A
	MOV	A,M
	ANI	0F0H
	CMP	C
	JZ	CFCVGLEI
	ANA	A
	JNZ	CFCVPKG
	INX	D	
	LDAX	D
	ANA	A
	RNZ						
	INX	H
	MOV	A,M
	ANA	A
	RET
CFCVPKG:
	INX	D
	LDAX	D
	ANA	A
	STC
	RNZ
	INX	H
	MOV	A,M
	ANA	A
	RZ
	STC
	RET
CFCVGLEI:
	INX	D
	INX	H
	LDAX	D
	CMP	M
	JZ	CF7CMP
	MOV	A,C
	JNC	CF8CMP
	ANA	A
	JNZ	CF9CMP
	INR	A
	RET
CF9CMP:
	STC
	RET
CF8CMP:
	ANA	A
	RNZ
	INR	A
	STC
	RET
CF7CMP:
	MOV	A,M
	ANA	A
	RZ
	MVI	B,7
CF10CMP:
	DCX	H
	DCX	D
	LDAX	D
	CMP	M
	JNZ	CF11CMP
	DCR	B
	JNZ	CF10CMP
	RET
CF11CMP:
	CMC
	RET
CFNEG:
	LXI	H,CFPRIM
	MVI	B,DMANT
	JMP	CFNEGATE
CFSNEG:
	XCHG
	LXI	H,CFSEC
	MVI	B,DMANT
	CALL	CFNEGATE
	XCHG
	RET
CFLNOT:
	LDA	CFPRIM+EXPONENT
	LXI	H,1
	ANA	A
	JZ	CFLN1
	DCR	L
	RET
CFLN1:
	ORA	L
	RET
CFSLNOT:
	LDA	CFSEC+EXPONENT
	LXI	D,1
	ANA	A
	JZ	CFSLN1
	DCR	E
	RET
CFSLN1:
	ORA	E
	RET
CFSFLAG:
	LDA	CFPRIM+EXPONENT
	ANA	A
	RET
CFSSFLAG:
	LDA	CFSEC+EXPONENT
	ANA	A
	RET
;
;	DSEG
RESULT:		DS 16
CFDRZIFF:	DS 2
CFMDVVOR:	DS 1
CFMRCOUNT:		DS 2
CFMLHZAE:		DS 1
CFADRU:			DS 1
;	CSEG
;
CFSSUB:
	CALL	CFSNEG
	JMP	CFADD
CFSUB:
	CALL	CFNEG		
CFADD:
	LXI	H,CFSEC+EXPONENT
	MOV	A,M		
	ANA	A		
	RZ
	LXI	D,CFPRIM+EXPONENT
	LDAX	D		
	ANA	A		
	JZ	CFADSNP		
	SUB	M		
	JNC	CFAD1FALL	
	CPI	0F3H		
 	JC	CFADSNP		
	CMA			
	INR	A		
	MOV	B,A
	DCX	D
	LDAX	D
	ANI	0F0H
	MOV	C,A
	LXI	H,CFSEC
	LXI	D,CFPRIM
	CALL	CFADDITION	
	JMP	CFADSNP		
CFAD1FALL:
	CPI	14		
 	RNC			
	MOV	B,A
	DCX	H
	MOV	A,M
	ANI	0F0H
	MOV	C,A
	LXI	H,CFPRIM
	LXI	D,CFSEC
;
CFADDITION:
	MOV	A,B
	ANA	A		
	JNZ	CFADD4
	MVI	C,50H		
	JMP	CFADD6
CFADD4:
	RAR			
	MOV	B,A		
	PUSH	PSW
	CMC
	SBI	0
	MOV	B,A	
	MOV	A,C
	ANA	A
	JZ	CFADD0
	MOV	A,B
	ANA	A
	JZ	CFADD2
	MOV	C,A
	XCHG
	XRA	A
CFADD3:
	ORA	M
	INX	H
	DCR	C
	JNZ	CFADD3
	XCHG
	MVI	C,50H
	ANA	A
	JNZ	CFADD1
CFADD2:
	MVI	C,40H
	JMP	CFADD1
CFADD0:
	MOV	A,B
	ADD	E	
	MOV	E,A	
	MVI	A,0
	ADC	D
	MOV	D,A
	MVI	C,50H
CFADD1:
	POP	PSW
	JC	CFAD21		
CFADD5:
	INR	B
CFADD6:
	CALL	CFADGRAZI	
	DCX	H		
	JMP	CFADNOR
CFAD21:
	CALL	CFADUNZI	
	DCX	H		
	JMP	CFADNOR
CFADSNP:
	LXI	H,CFPRIM
	LXI	D,CFSEC
	MVI	B,8		
CFAD22:
	LDAX	D
	MOV	M,A
	INX	H
	INX	D
	DCR	B
	JNZ	CFAD22
	RET
CFADNOR:
	MOV	A,M		
	ANI	0F0H
	CPI	80H		
	JZ	CFAD30
	CPI	10H		
	JZ	CFAD30
	INX	H		
	JMP	CFNORM		
CFADUNZI:
	LDAX	D
	RLC
	RLC
	RLC
	RLC
	ADD	C
	DAA
	PUSH	B	
	PUSH	PSW	
	MVI	A,6	
	SUB	B
	JZ	CFAD10	
	MOV	B,A
CFAD9:
	LDAX	D	
	RAR		
	RAR		
	RAR		
	RAR
	ANI	0FH
	MOV	C,A
	INX	D
	LDAX	D
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ORA	C
	MOV	C,A
	POP	PSW	
	MOV	A,C
	ADC	M	
	DAA
	MOV	M,A
	INX	H
	DCR	B
	PUSH	PSW	
	JNZ	CFAD9
CFAD10:
	POP	PSW	
	POP	B	
	INR	B	
	PUSH	PSW	
	LDAX	D	
	RAR
	RAR
	RAR
	RAR
	ANI	0FH
	CPI	09H	
	JM	CFAD11	
	MVI	C,99H	
	JMP	CFAD12
CFAD11:
	MVI	C,0
CFAD12:
	POP	PSW	
CFAD13:
	MOV	A,C
	ADC	M
	DAA
	MOV	M,A
	INX	H
	DCR	B
	JNZ	CFAD13
	RET
CFADGRAZI:
	MOV	A,B	
	ANA	A	
	PUSH	PSW	
	JZ	CFAD14	
	MVI	A,7	
	SUB	B
	MOV	B,A
	LDAX	D	
	INX	D	
	ADD	C
	DAA
	JMP	CFAD15
CFAD14:			
	MVI	B,DMANT	
CFAD15:
	LDAX	D
	ADC	M
	DAA
	MOV	M,A
	INX	D
	INX	H
	DCR	B
	JNZ	CFAD15
	PUSH	PSW	
	DCX	D	
	LDAX	D
	ANI	0F0H
	CPI	90H
	JZ	CFAD16	
	MOV	C,A	
	JMP	CFAD17
CFAD16:
	MVI	C,99H
CFAD17:
	POP	PSW	
	MOV	A,C
	POP	B	
	INR	B
	DCR	B
	RZ
	MOV	C,A
CFAD18:
	MOV	A,C	
	ADC	M
	DAA
	MOV	M,A
	INX	H
	DCR	B
	JNZ	CFAD18
	RET
CFAD30:
	LXI	B,-6
	DAD	B
CFAD81:
	MVI	B,6	
	MOV	D,H
	MOV	E,L
	MOV	A,M	
	ANI	0FH	
	CPI	5
	CMC		
	JNC	CFAD2
CFAD1:
	PUSH	PSW	
	LDAX	D	
	RAR
	RAR
	RAR
	RAR
	ANI	0FH	
	MOV	C,A	
	INX	D
	LDAX	D
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ORA	C
	MOV	C,A
	POP	PSW	
	MVI	A,0
	ADC	C
	DAA
	MOV	M,A
	INX	H
	DCR	B
	JZ	CFAD3
	JC	CFAD1
CFAD2:
	LDAX	D
	RAR
	RAR
	RAR
	RAR
	ANI	0FH
	MOV	C,A
	INX	D
	LDAX	D
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ADD	C
	MOV	M,A
	INX	H
	DCR	B
	JNZ	CFAD2
	ANA	A	
CFAD3:
	PUSH	PSW	
	LDAX	D
	RAR
	RAR
	RAR
	RAR
	ANI	0FH	
	CPI	1
	MOV	C,A
	JZ	CFAD4
	POP	PSW	
	MVI	A,90H
	JMP	CFAD5
CFAD4:
	POP	PSW	
	MVI	A,0
CFAD5:
	ADC	C
	DAA
	MOV	M,A
	INX	H	
	INR	M
	RNZ
	MOV	A,C
	CPI	1
	JNZ	CFAD90
	CALL	CFOVERFLOW
	JMP	CFSTMAX
CFAD90:
	CALL	CFOVERFLOW
	JMP	CFSTMIN
CFSHL:
	MOV	A,B
	ANA	A
	RZ		
	RAR
	MOV	B,A	
	CMA		
	INR	A	
	JC	CFZSHL	
	MVI	D,0FFH
	MOV	E,A	
	XCHG		
	DAD	D	
	ADD	C	
	MOV	C,A	
CFSL1:
	MOV	A,M	
	STAX	D
	DCX	H
	DCX	D
	DCR	C
	JNZ	CFSL1
CFSLFILL:
	XRA	A	
CFSL2:
	STAX	D
	DCX	D
	DCR	B
	JNZ	CFSL2
	RET
CFZSHL:			
	DCR	A
	MVI	D,0FFH
	MOV	E,A	
	INX	D	
	XCHG		
	DAD	D	
	ADD	C	
	MOV	C,A	
	PUSH	B	
	MOV	A,M	
	RAL		
	RAL
	RAL
	RAL
	STAX	D	
	DCX	H
CFSL3:
	LDAX	D	
	ANI	0F0H
	MOV	B,A
	MOV	A,M	
	RAR
	RAR
	RAR
	RAR
	ANI	0FH
	ORA	B	
	STAX	D
	DCX	D
	MOV	A,M	
	RAL		
	RAL		
	RAL
	RAL
	STAX	D
	DCX	H
	DCR	C
	JNZ	CFSL3	
	POP	B	
	ANI	0F0H	
	STAX	D	
	DCX	D
	DCR	B
	INR	B	
 	JNZ	CFSLFILL	
	RET
CFNORM:
	MOV 	A,M	
	ANA	A
	RZ		
	DCX	H
	MOV	A,M
	ANI	0F0H	
	JNZ	CFNONEG	
	MOV	A,M	
	ANI	0FH	
	RNZ		
	PUSH	H	
	MVI	B,DMANT+DMANT-2
	DCX	H	
CFNO1:
	MOV	A,M
	ANA	A
	JNZ	CFNO2	
	DCX	H
	DCR	B	
	DCR	B
	JNZ	CFNO1
CFNOG0:
	POP	H
	INX	H
	MVI	M,0	
	RET
CFNO2:
	ANI	0F0H
	JNZ	CFNOUNGLEICH	
	DCR	B	
CFNOUNGLEICH:
	MVI	A,DMANT+DMANT-1
	SUB	B	
	MOV	B,A	
	POP	H	
	INX	H	
	MOV	A,M
	SUB	B	
	JC	CFNO3	
	JZ	CFNO3	
	MOV	M,A	
	DCX	H	
	MOV	A,M
	ANI	0F0H
	PUSH	PSW
	PUSH	H
	MVI	C,DMANT	
	CALL	CFSHL	
	POP	H	
	MOV	A,M
	ANI	0FH
	MOV	M,A
	POP	PSW	
	ORA	M	
	MOV	M,A
	RET
CFNO3:			
	MVI	M,0	
	JMP	CFUNDERFLOW
CFNONEG:
	MOV	A,M	
	CPI	99H	
	JZ	CFNO5	
	ANI	0FH	
	RNZ		
	PUSH	H	
	MVI	B,DMANT-1	
	DCX	H
CFNO6:
	MOV	A,M
	ANA	A
	JNZ	CFNO61
	DCX	H
	DCR	B
	JNZ	CFNO6
	POP	H	
	MVI	M,99H
	INX	H
	INR	M
	RNZ
	CALL	CFSTMIN
	JMP	CFOVERFLOW
CFNO61:
	POP	H
	RET
CFNO5:		
	PUSH	H	
	MVI	B,DMANT+DMANT-2
	DCX	H	
CFNO51:
	MOV	A,M
	CPI	99H
	JNZ	CFNO52	
	DCX	H
	DCR	B	
	DCR	B
	JNZ	CFNO51
	JMP	CFNO00
CFNO52:
	ANI	0F0H
	MOV	C,B
	CPI	90H
	JNZ	CFNONUNGL	
	DCR	B	
	MOV	A,M
	ANI	0FH
	JNZ	CFNON0	
	DCR	C
	DCX	H
CFNONUNGL:
	MOV	A,C
	ANA	A
	JZ	CFNO00	
CFNO54:
	MOV	A,M
	ANA	A
	JNZ	CFNON0	
	DCX	H
	DCR	C
	DCR	C
	JNZ	CFNO54
CFNO00:
	INR	B	
CFNON0:
	JMP	CFNOUNGLEICH
CFNEGATE:
	XRA	A	
	MOV	C,A	
CFNE1:	MOV	A,C	
	SBB	M
	DAA
	MOV	M,A	
	INX	H
	DCR	B
	JNZ	CFNE1
	RET
CFSTMAX:
	PUSH	H	
	PUSH	B
	MVI	M,0FFH	
	DCX	H
	MVI	M,9	
	MVI B,DMANT-1	
CFSMA1:
	DCX	H
	MVI	M,99H
	DCR	B
	JNZ	CFSMA1
	POP	B
	POP	H
	RET
CFSTMIN:
	PUSH	H	
	PUSH	B
	MVI	M,1	
	DCX	H
	MVI	M,90H	
	MVI B,DMANT-2	
CFSMI1:
	DCX	H
	MVI	M,0
	DCR	B
	JNZ	CFSMI1
	MVI	M,01H
	POP	B
	POP	H
	RET
CFDSUB:
	LXI	D,CFSEC
	LXI	H,CFPRIM
	MVI	B,DMANT	
	ANA	A
CFDS1:			
	LDAX	D
	SBB	M
	DAA
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	CFDS1
	RET
CFDCOMPAR:
	LXI	H,CFSEC+VORZEI	
	LXI	D,CFPRIM+VORZEI
CFDC1:			
			
	LDAX	D
	CMP	M	
	RC		
	RNZ		
	DCX	H
	DCX	D
	DCR	B
	JNZ	CFDC1
	STC		
	RET
CFSDIV:
	CALL	CFSWAP
CFDIV:
	LDA	CFPRIM+EXPONENT
	ANA	A	
	JZ	CF0DIV	
	LDA	CFSEC+EXPONENT
	ANA	A	
	JNZ	CFDV1	
	STA	CFPRIM+EXPONENT
	RET
CFDV1:
	CALL	CFMDVORZ
	CALL	CFDCOMPAR
	MVI	A,81H
	PUSH	PSW
	JC	CFDV2	
	POP	PSW
	DCR A
	PUSH	PSW	
	LXI	H,CFSEC+VORZEI
	MVI	C,DMANT	
	MVI	B,1	
	CALL	CFSHL	
CFDV2:
	MVI	A,DMANT	
	PUSH	PSW	
	LXI	H,RESULT+RMANT-1
	SHLD	CFDRZIFF	
	MVI	M,0	
CFDVMLOP:
CFDVM1:
	CALL	CFDCOMPAR
	JNC	CFDVM2	
	CALL	CFDSUB	
	LHLD	CFDRZIFF	
	INR	M	
	JMP	CFDVM1	
CFDVM2:
	LXI	H,CFSEC+VORZEI
	MVI	C,DMANT	
	MVI	B,1	
	CALL	CFSHL	
	LHLD	CFDRZIFF
	DCX	H	
	SHLD	CFDRZIFF
	MVI	M,0	
CFDVM3:
	CALL	CFDCOMPAR
	JNC	CFDVM4	
	CALL	CFDSUB	
	LHLD	CFDRZIFF	
	MVI	A,10H
	ADD	M	
	MOV	M,A
	JMP	CFDVM3	
CFDVM4:
	POP	PSW	
	DCR	A
	JZ	CFDVMEND	
	PUSH	PSW	
	LXI	H,CFSEC+VORZEI
	MVI	C,DMANT	
	MVI	B,1	
	CALL	CFSHL	
	JMP	CFDVMLOP
CFDVMEND:
	LXI	H,RESULT+RMANT-DMANT-1
	LXI	D,CFPRIM
	MVI	B,DMANT	
	MOV	A,M
	INX	H
	CPI	50H	
	CMC		
	JNC	CFDV4	
CFDV5:
	MVI	A,0
	ADC	M
	DAA
	STAX	D
	INX	H
	INX	D
	DCR	B
	JC	CFDV5	
	JZ	CFDV42	
CFDV4:
	MOV	A,M
	STAX	D
	INX	H
	INX	D
	DCR	B
	JNZ	CFDV4
CFDV41:
	XCHG		
	MOV	A,M
	CMA
	INR	A	
	MOV	M,A	
	POP	PSW	
	JMP	CFMDKX	
CFDV42:
	DCX	D
	LDAX	D
	CPI	10H
	INX	D
	JNZ	CFDV41
	XCHG
	DCX	H
	MVI	M,1
	INX	H
	MOV	A,M	
	CMA
	INR	A
	MOV	M,A
	POP	PSW	
	INR	A	
	JMP	CFMDKX
CFMULT:
	LXI	H,CFPRIM+EXPONENT
	MOV	A,M
	ANA	A
	RZ
	LDA	CFSEC+EXPONENT
	ANA	A
	JNZ	CFML15
	MOV	M,A	
	RET
CFML15:
	CALL	CFMDVORZ
	MVI	B,DMANT+DMANT
	LXI	H,RESULT
	XRA	A
CFML1:
	MOV	M,A
	INX	H
	DCR	B
	JNZ	CFML1
	LXI	D,0
	PUSH	D
CFMLNZIFF:
	POP	B		
	MOV	A,C		
	CPI	7
	JZ	CFMLEND
	LXI	H,RESULT	
	DAD	B		
	SHLD	CFMRCOUNT	
	LXI	H,CFSEC	
	DAD	B		
	INR	C		
	PUSH	B		
	MOV	A,M
	RAR
	RAR
	RAR
	RAR
	ANI	0FH		
	PUSH	PSW
	MOV	A,M
	ANI	0FH		
	PUSH	PSW
CFMLABYT:
	POP	PSW		
	JZ	CFMLAHABYT	
	DCR	A
	PUSH	PSW
	LHLD	CFMRCOUNT	
	MVI	B,DMANT		
	LXI	D,CFPRIM
	ANA	A		
CFML2:
	LDAX	D
	INX	D
	ADC	M
	DAA
	MOV	M,A
	INX	H
	DCR	B
	JNZ	CFML2
	JNC	CFMLABYT
CFML30:
	MOV	A,B
	ADC	M
	DAA
	MOV	M,A
	INX	H
	JC	CFML30
	JMP	CFMLABYT
CFMLAHABYT:
	LHLD	CFMRCOUNT
	POP	PSW		
	JZ	CFMLNZIFF	
	DCR	A
	PUSH	PSW
	LXI	D,CFPRIM
	LDAX	D
	INX	D
	MOV	B,A
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ADC	M
	DAA
	MOV	M,A
	PUSH	PSW		
	MVI	A,DMANT-1	
	STA	CFMLHZAE
	INX	H
CFMLSCH2:
	MOV	A,B		
	RAR
	RAR
	RAR
	RAR
	ANI	0FH
	MOV	B,A
	LDAX	D		
	MOV	C,A
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ADD	B		
	MOV	B,A
	POP	PSW		
	MOV	A,B
	ADC	M
	DAA
	MOV	M,A
	MOV	B,C
	INX	H
	INX	D
	PUSH	PSW		
	LDA	CFMLHZAE	
	DCR	A		
	STA	CFMLHZAE	
	JNZ	CFMLSCH2
	POP	PSW		
	JNC	CFMLAHABYT
CFMLADDH0:
	MVI	A,0
	ADC	M
	DAA
	MOV	M,A
	INX	H
	JC	CFMLADDH0
	JMP	CFMLAHABYT
CFMLEND:
	LDA	RESULT+DMANT+DMANT-2
	ANI	0F0H
	JNZ	CFML5
	LXI	H,RESULT+DMANT-2
	MOV	A,M
	INX	H
	CPI	50H
	CMC
	MVI	B,DMANT		
	LXI	D,CFPRIM
	JNC	CFML6
CFML7:
	MVI	A,0
	ADC	M
	DAA
	STAX	D
	INX	D
	INX	H
	DCR	B
	JC	CFML7
	JZ	CFML8
CFML6:
	MOV	A,M
	STAX	D
	INX	D
	INX	H
	DCR	B
	JNZ	CFML6
CFML8:
	LXI	H,CFPRIM+VORZEI
	MOV	A,M
	INX	H
	ANI	0F0H	
	MVI	A,7FH	
	JZ	CFMDKX
	DCX	H
	MVI	M,1
	INX	H
	INR	A
	JMP	CFMDKX
CFML5:
	LXI	H,RESULT+DMANT-1	
	MOV	A,M
	ANI	0FH		
	CPI	5		
	CMC			
	PUSH	PSW		
	MVI	B,DMANT 	
	LXI	D,CFPRIM
	JNC	CFML10
CFML9:
	MOV	A,M		
	RAR
	RAR
	RAR
	RAR
	ANI	0FH		
	MOV	C,A		
	INX	H
	MOV	A,M		
	RAL
	RAL
	RAL
	RAL			
	ANI	0F0H		
	ORA	C
	MOV	C,A
	POP	PSW		
	MVI	A,0
	ADC	C
	DAA
	STAX	D
	PUSH	PSW		
	INX	D
	DCR	B
	JC	CFML9
	JZ	CFML11
CFML10:
	MOV	A,M
	RAR
	RAR
	RAR
	RAR
	ANI	0FH
	MOV	C,A
	INX	H
	MOV	A,M
	RAL
	RAL
	RAL
	RAL
	ANI	0F0H
	ORA	C
	STAX	D
	INX	D
	DCR	B
	JNZ	CFML10
CFML11:
	LXI	H,CFPRIM+DMANT-1 
	POP	PSW		  
	MOV	A,M
	ANI	0F0H
	JNZ	CFML12
	MVI	A,80H		
	INX	H	
	JMP	CFMDKX
CFML12:
	MVI	A,1
	MOV	M,A
	MVI	A,80H
	INX	H
CFMDKX:
	ADD	M
	MOV	C,A
	LDA	CFSEC+EXPONENT
	JC	CFDV3	
	ADD	C
	MOV	M,A
	JC	CFDV8	
	CALL	CFUNDERFLOW
	RET
CFDV3:			
	ADD	C
	MOV	M,A
	JC	CFDV7
CFDV8:
	LDA	CFMDVVOR	
	ANA	A
	LXI	H,CFPRIM
	MVI	B,7
	RZ
	JMP	CFNEGAT
CFDV7:
	CALL	CFOVERFLOW	
	CALL	CFSTMAX
	JMP	CFDV8	
CFMDVORZ:
	LXI	H,CFPRIM+VORZEICHEN
	LXI	D,CFSEC+VORZEICHEN
	LDAX	D
	XRA	M
	ANI	0F0H
	STA	CFMDVVOR
	MOV	A,M
	ANI	0F0H
	JZ	CFML13
	LXI	H,CFPRIM
	MVI	B,7
	CALL	CFNEGATE
CFML13:
	LXI	D,CFSEC+VORZEICHEN
	LDAX	D
	ANI	0F0H
	RZ
	LXI	H,CFSEC
	MVI	B,7
	JMP	CFNEGATE
;
